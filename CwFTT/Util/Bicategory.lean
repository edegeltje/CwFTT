import Mathlib.CategoryTheory.Bicategory.Strict.Basic
import Mathlib.CategoryTheory.Bicategory.Functor.Pseudofunctor
import Mathlib.CategoryTheory.Bicategory.Opposites

/-!
given a strict bicategory B, we get a functor `B·µí·µñ ‚•§ B ‚•§ Cat`
-/

universe w w‚ÇÇ w‚ÇÉ v v‚ÇÇ v‚ÇÉ u u‚ÇÇ u‚ÇÉ
namespace CategoryTheory
variable (B : Type u) [Bicategory.{w, v} B]
namespace Bicategory

section
def coyoneda [Bicategory.Strict B] : B·µí·µñ ‚•§ B ‚•§ Cat where
  obj X := {
    obj Y := .of <| X.unop ‚ü∂ Y
    map {Y‚ÇÅ Y‚ÇÇ} f := postcomp X.unop f
    map_id Y := Functor.ext (by simp) (by simp [Strict.rightUnitor_eqToIso])
    map_comp {Y‚ÇÅ Y‚ÇÇ Y‚ÇÉ} f g := Functor.ext (by simp) (by simp [Strict.associator_eqToIso])
  }
  map {X‚ÇÅ X‚ÇÇ} f := {
    app Y := precomp Y f.unop
    naturality {Y‚ÇÅ Y‚ÇÇ} g := Functor.ext (by simp) (by simp [Strict.associator_eqToIso])
  }
  map_id X := by
    ext Y
    apply Functor.ext (by simp) (by simp [Strict.leftUnitor_eqToIso])
  map_comp {X‚ÇÅ X‚ÇÇ X‚ÇÉ} f g := by
    ext Y
    apply Functor.ext (by simp) (by simp [Strict.associator_eqToIso])
end

section PseudoNatTrans
variable (B‚ÇÇ : Type u‚ÇÇ) [Bicategory.{w‚ÇÇ, v‚ÇÇ} B‚ÇÇ] (F G : B ‚•§·µñ B‚ÇÇ)
variable {B B‚ÇÇ}

structure PseudoNatTrans where
  app (X : B) : F.obj X ‚ü∂ G.obj X
  app‚ÇÇ {X Y : B} (f : X ‚ü∂ Y) : (F.map f ‚â´ app Y) ‚âÖ (app X ‚â´ G.map f)
  app‚ÇÇ_naturality {X Y : B} {f g : X ‚ü∂ Y} (h : f ‚ü∂ g) :
    (F.map‚ÇÇ h) ‚ñ∑ app Y ‚â´ (app‚ÇÇ g).hom = (app‚ÇÇ f).hom ‚â´ app X ‚óÅ (G.map‚ÇÇ h) := by cat_disch
  unitality (X : B) :
    (rightUnitor (app X)).hom ‚â´ (leftUnitor (app X)).inv ‚â´ (F.mapId _).inv ‚ñ∑ app X =
    app X ‚óÅ (G.mapId X).inv ‚â´ (app‚ÇÇ (ùüô X)).inv := by cat_disch
  associativity {X‚ÇÅ X‚ÇÇ X‚ÇÉ : B} (f : X‚ÇÅ ‚ü∂ X‚ÇÇ) (g : X‚ÇÇ ‚ü∂ X‚ÇÉ) :
    ((app‚ÇÇ f).inv ‚ñ∑ (G.map g)) ‚â´ (associator _ _ _).hom ‚â´ (F.map f ‚óÅ (app‚ÇÇ g).inv) ‚â´
      (associator _ _ _).inv ‚â´ (F.mapComp f g).inv ‚ñ∑ _ =
    (associator _ _ _).hom ‚â´ _ ‚óÅ (G.mapComp f g).inv ‚â´
      (app‚ÇÇ (f ‚â´ g)).inv := by cat_disch

instance : Quiver (B ‚•§·µñ B‚ÇÇ) where
  Hom {F G} := PseudoNatTrans F G

variable {F G}

variable (Œ± Œ≤ : F ‚ü∂ G)

structure PseudoModification where
  mod (X : B) : Œ±.app X ‚ü∂ Œ≤.app X
  mod_naturality {X Y : B} (f : X ‚ü∂ Y) :
    mod X ‚ñ∑ G.map f ‚â´ (Œ≤.app‚ÇÇ f).inv = (Œ±.app‚ÇÇ f).inv ‚â´ (F.map f ‚óÅ (mod Y)) := by
    cat_disch

variable {Œ± Œ≤}

namespace PseudoModification

instance : Quiver (F ‚ü∂ G) where
  Hom {Œ± Œ≤} := PseudoModification Œ± Œ≤

@[ext]
lemma ext (a b : Œ± ‚ü∂ Œ≤) (h : ‚àÄ X, a.mod X = b.mod X) : a = b := by
  cases a ; cases b; congr; ext ; simp_all

def comp {Œ≥ : F ‚ü∂ G} (a : Œ± ‚ü∂ Œ≤) (b : Œ≤ ‚ü∂ Œ≥) : Œ± ‚ü∂ Œ≥ where
  mod X := a.mod X ‚â´ b.mod X
  mod_naturality {X Y : B} f := by
    rw [comp_whiskerRight_assoc,b.mod_naturality,
      reassoc_of% a.mod_naturality,whiskerLeft_comp]

lemma assoc {Œ≥ Œ¥ : F ‚ü∂ G} (a : Œ± ‚ü∂ Œ≤) (b : Œ≤ ‚ü∂ Œ≥) (c : Œ≥ ‚ü∂ Œ¥) :
    comp (comp a b) c = comp a (comp b c) := by
  ext X ; simp [comp]

variable (Œ±) in
def id : Œ± ‚ü∂ Œ± where
  mod X := (ùüô (Œ±.app X))

lemma comp_id (a : Œ± ‚ü∂ Œ≤) : a.comp (.id Œ≤) = a := by
  ext X; simp [id,comp]

lemma id_comp (a : Œ± ‚ü∂ Œ≤) : comp (id Œ±) a = a := by
  ext X; simp [id,comp]

instance : Category (F ‚ü∂ G) where
  id X := id X
  comp f g := comp f g
  id_comp := id_comp
  comp_id := comp_id
  assoc := assoc

variable (Œ±) in
@[simp]
lemma id_mod_app (X : B) : PseudoModification.mod (ùüô Œ±) X = ùüô (Œ±.app X) := rfl

@[simp]
lemma comp_mod_app {Œ≥ : F ‚ü∂ G} (a : Œ± ‚ü∂ Œ≤) (b : Œ≤ ‚ü∂ Œ≥) (X : B) :
    (a ‚â´ b).mod X = a.mod X ‚â´ b.mod X := rfl

@[simp]
lemma eqToHom_mod (h : Œ± = Œ≤) (X : B) :
    (eqToHom h).mod X = eqToHom congr(($h).app X) := by
  cases h
  simp


instance [‚àÄ X Y : B‚ÇÇ, Quiver.IsThin (X ‚ü∂ Y)] : Quiver.IsThin (F ‚ü∂ G) :=
  fun Œ± Œ≤ => {
    allEq a b := by
      ext X
      rename_i h
      apply Subsingleton.elim
  }

end PseudoModification

namespace PseudoNatTrans

@[simps]
def ext {Œ± Œ≤ : F ‚ü∂ G} (appIso : ‚àÄ X, Œ±.app X ‚âÖ Œ≤.app X)
    (happIso_nat : ‚àÄ {X Y : B} (f : X ‚ü∂ Y), (appIso X).hom ‚ñ∑ G.map f ‚â´ (Œ≤.app‚ÇÇ f).inv =
      (Œ±.app‚ÇÇ f).inv ‚â´ F.map f ‚óÅ (appIso Y).hom) :
    Œ± ‚âÖ Œ≤ where
  hom := {
    mod X := (appIso X).hom
    mod_naturality {X Y} f := happIso_nat f
  }
  inv := {
    mod X := (appIso X).inv
    mod_naturality {X Y} f := by
      rw [‚Üê whiskerRightIso_inv, ‚Üê whiskerLeftIso_inv, Iso.eq_comp_inv, Category.assoc,
        Iso.inv_comp_eq, whiskerLeftIso_hom,‚Üê happIso_nat, whiskerRightIso_hom]
  }
  hom_inv_id := by ext X; simp
  inv_hom_id := by ext X; simp

@[simps]
def _root_.CategoryTheory.Iso.mod {Œ± Œ≤ : F ‚ü∂ G} (e : Œ± ‚âÖ Œ≤) (X : B) : Œ±.app X ‚âÖ Œ≤.app X where
  hom := e.hom.mod X
  inv := e.inv.mod X
  hom_inv_id := congr(($e.hom_inv_id).mod X)
  inv_hom_id := congr(($e.inv_hom_id).mod X)

lemma _root_.CategoryTheory.Iso.mod_hom_naturality {Œ± Œ≤ : F ‚ü∂ G} (e : Œ± ‚âÖ Œ≤) {X Y : B} (f : X ‚ü∂ Y) :
    (e.mod X).hom ‚ñ∑ G.map f ‚â´ (Œ≤.app‚ÇÇ f).inv = (Œ±.app‚ÇÇ f).inv ‚â´ F.map f ‚óÅ (e.mod Y).hom :=
  e.hom.mod_naturality f

lemma _root_.CategoryTheory.Iso.mod_inv_naturality {Œ± Œ≤ : F ‚ü∂ G} (e : Œ± ‚âÖ Œ≤) {X Y : B} (f : X ‚ü∂ Y) :
    (e.mod X).inv ‚ñ∑ G.map f ‚â´ (Œ±.app‚ÇÇ f).inv = (Œ≤.app‚ÇÇ f).inv ‚â´ F.map f ‚óÅ (e.mod Y).inv :=
  e.inv.mod_naturality f

def ext! {Œ± Œ≤ : F ‚ü∂ G} (happ : ‚àÄ X, Œ±.app X = Œ≤.app X) (happ‚ÇÇ : ‚àÄ {X Y : B} (f : X ‚ü∂ Y),
  (Œ±.app‚ÇÇ f).hom = eqToHom congr(F.map f ‚â´ $(happ Y)) ‚â´ (Œ≤.app‚ÇÇ f).hom ‚â´
    eqToHom (congr($(happ X) ‚â´ G.map f)).symm) : Œ± = Œ≤ := by
  cases Œ±;cases Œ≤;
  simp at happ happ‚ÇÇ ‚ä¢
  cases funext happ
  simp_all
  congr
  ext X Y f
  rw [happ‚ÇÇ]


@[reassoc]
def associativity_symm (Œ± : F ‚ü∂ G) {X‚ÇÅ X‚ÇÇ X‚ÇÉ} (f : X‚ÇÅ ‚ü∂ X‚ÇÇ) (g : X‚ÇÇ ‚ü∂ X‚ÇÉ) :
    (Œ±.app X‚ÇÅ) ‚óÅ (G.mapComp f g).inv ‚â´ (Œ±.app‚ÇÇ (f ‚â´ g)).inv =
    (Œ±_ _ _ _).inv ‚â´ ((Œ±.app‚ÇÇ f).inv ‚ñ∑ (G.map g)) ‚â´ (Œ±_ _ _ _).hom ‚â´
    (F.map f ‚óÅ (Œ±.app‚ÇÇ g).inv) ‚â´ (Œ±_ _ _ _).inv ‚â´ (F.mapComp f g).inv ‚ñ∑ _ := by
  simp [associativity]

def comp {H : B ‚•§·µñ B‚ÇÇ} (Œ± : F ‚ü∂ G) (Œ≤ : G ‚ü∂ H) : F ‚ü∂ H where
  app X := Œ±.app X ‚â´ Œ≤.app X
  app‚ÇÇ {X Y} f := (associator _ _ _).symm ‚â™‚â´ whiskerRightIso (Œ±.app‚ÇÇ _) (Œ≤.app Y) ‚â™‚â´
    (associator _ _ _) ‚â™‚â´ whiskerLeftIso _ (Œ≤.app‚ÇÇ _) ‚â™‚â´ (associator _ _ _).symm
  app‚ÇÇ_naturality {X Y} {f g} h := by
    simp only [whiskerRight_comp, Iso.trans_hom, Iso.symm_hom, whiskerRightIso_hom,
      whiskerLeftIso_hom, Category.assoc, Iso.hom_inv_id_assoc, comp_whiskerLeft,
      Iso.inv_hom_id_assoc, Iso.cancel_iso_inv_left]
    rw [‚Üê comp_whiskerRight_assoc, Œ±.app‚ÇÇ_naturality,comp_whiskerRight,whisker_assoc,
      Category.assoc,Category.assoc,Category.assoc,
      Iso.inv_hom_id_assoc,‚Üê whiskerLeft_comp_assoc,Œ≤.app‚ÇÇ_naturality,
        whiskerLeft_comp_assoc]
  unitality X := by
    simp only [Iso.trans_inv, Iso.symm_inv, whiskerLeftIso_inv,
      Category.assoc, whiskerRightIso_inv]
    rw [associator_naturality_right_assoc, ‚Üê whiskerLeft_comp_assoc, ‚Üê Œ≤.unitality]
    simp only [whiskerRight_comp, whiskerLeft_comp, whiskerLeft_rightUnitor, Category.assoc,
      Iso.hom_inv_id_assoc, Iso.cancel_iso_hom_left]
    rw [@associator_inv_naturality_middle_assoc,
      ‚Üê comp_whiskerRight_assoc, ‚Üê Œ±.unitality]
    simp
  associativity {X‚ÇÅ X‚ÇÇ X‚ÇÉ} f g := by
    simp only [Iso.trans_inv, Iso.symm_inv, whiskerLeftIso_inv, Category.assoc, whiskerRightIso_inv]
    rw [comp_whiskerLeft_assoc, Iso.inv_hom_id_assoc]
    simp only [comp_whiskerRight, whisker_assoc, Category.assoc, whiskerLeft_comp,
      whiskerRight_comp, pentagon_hom_inv_inv_inv_inv_assoc, pentagon_assoc]
    rw [‚Üê whiskerLeft_comp_assoc (Œ±.app X‚ÇÅ), Œ≤.associativity_symm]
    simp only [whiskerLeft_comp, Category.assoc, pentagon_hom_hom_inv_hom_hom_assoc]
    rw [whisker_assoc_symm_assoc (Œ±.app X‚ÇÅ) (G.mapComp f g).inv (Œ≤.app X‚ÇÉ),
      Iso.hom_inv_id_assoc,‚Üê comp_whiskerRight_assoc _ ((Œ±.app‚ÇÇ (f ‚â´ g)).inv) (Œ≤.app X‚ÇÉ),
      Œ±.associativity_symm]
    simp only [comp_whiskerRight, whisker_assoc, Category.assoc, pentagon_inv_assoc]
    simp_rw [associator_naturality_left_assoc, associator_inv_naturality_right_assoc,
      pentagon_inv_inv_hom_hom_inv_assoc]
    simp only [‚Üê Category.assoc]
    congr 4
    simp only [Category.assoc]
    congr 3
    rw [‚Üê pentagon_hom_hom_inv_hom_hom_assoc, associator_naturality_left_assoc,
      Iso.inv_hom_id_assoc, whisker_exchange_assoc]
    simp

variable (F) in
def id : F ‚ü∂ F where
  app X := ùüô _
  app‚ÇÇ {X Y} f := (œÅ_ (F.map f)) ‚â™‚â´ (Œª_ (F.map f)).symm

def whiskerLeft {H : B ‚•§·µñ B‚ÇÇ} (Œ≥ : H ‚ü∂ F) (a : Œ± ‚ü∂ Œ≤) : comp Œ≥ Œ± ‚ü∂ comp Œ≥ Œ≤ where
  mod X := Œ≥.app X ‚óÅ a.mod X
  mod_naturality {X Y} f := by
    simp only [comp, whisker_assoc, Iso.trans_inv, Iso.symm_inv, whiskerLeftIso_inv,
      Category.assoc, whiskerRightIso_inv, Iso.inv_hom_id_assoc, Iso.cancel_iso_hom_left]
    rw [‚Üê whiskerLeft_comp_assoc,a.mod_naturality]
    simp only [whiskerLeft_comp, Category.assoc]
    rw [@associator_inv_naturality_right_assoc,whisker_exchange_assoc]
    simp

def whiskerRight {H : B ‚•§·µñ B‚ÇÇ} (a : Œ± ‚ü∂ Œ≤) (Œ≥ : G ‚ü∂ H) : comp Œ± Œ≥ ‚ü∂ comp Œ≤ Œ≥ where
  mod X := a.mod X ‚ñ∑ Œ≥.app X
  mod_naturality {X Y} f := by
    simp only [comp, Iso.trans_inv, Iso.symm_inv, whiskerLeftIso_inv, Category.assoc,
      whiskerRightIso_inv]
    rw [@associator_naturality_left_assoc,‚Üê whisker_exchange_assoc]
    simp only [whiskerRight_comp, Category.assoc, Iso.hom_inv_id_assoc, Iso.cancel_iso_hom_left]
    rw [‚Üê comp_whiskerRight_assoc,a.mod_naturality]
    simp

-- variable (Œ± Œ≤) in
def associator {F G H I : B ‚•§·µñ B‚ÇÇ} (Œ± : F ‚ü∂ G) (Œ≤ : G ‚ü∂ H) (Œ≥ : H ‚ü∂ I) :
    comp (comp Œ± Œ≤) Œ≥ ‚âÖ comp Œ± (comp Œ≤ Œ≥) :=
  ext (fun X => Œ±_ (Œ±.app X) (Œ≤.app X) (Œ≥.app X)) (by simp [comp])

variable (Œ±) in
def leftUnitor : comp (id F) Œ± ‚âÖ Œ± :=
  ext (fun X => Œª_ (Œ±.app X)) (by simp [comp, id])

variable (Œ±) in
def rightUnitor : comp Œ± (id G) ‚âÖ Œ± :=
  ext (fun X => œÅ_ (Œ±.app X)) (by simp [comp, id])

instance _root_.Pseudofunctor.instBicategory : Bicategory (B ‚•§·µñ B‚ÇÇ) where
  id := id
  comp := comp
  whiskerLeft f Œ± _ h := whiskerLeft f h
  whiskerRight := whiskerRight
  associator {F G H I} := associator
  leftUnitor := leftUnitor
  rightUnitor := rightUnitor
  whiskerLeft_id := by intros; ext X; simp [comp, whiskerLeft]
  whiskerLeft_comp := by intros; ext X; simp [whiskerLeft]
  id_whiskerLeft := by intros; ext X; simp [comp, id, whiskerLeft, leftUnitor]
  comp_whiskerLeft := by intros; ext X; simp [comp, whiskerLeft, associator]
  id_whiskerRight := by intros; ext X; simp [comp, whiskerRight]
  comp_whiskerRight := by intros; ext X; simp [whiskerRight]
  whiskerRight_id := by intros; ext X; simp [id, whiskerRight, rightUnitor]
  whiskerRight_comp := by intros; ext X; simp [comp, whiskerRight, associator]
  whisker_assoc := by intros; ext X; simp [whiskerLeft, whiskerRight, associator]
  whisker_exchange := by intros; ext X; simpa [whiskerLeft, whiskerRight] using whisker_exchange _ _
  pentagon := by intros; ext X; simp [comp, whiskerLeft, whiskerRight, associator]
  triangle := by intros; ext X; simp [id, whiskerLeft, whiskerRight, leftUnitor, rightUnitor,
    associator]

@[simp]
lemma comp_app {H : B ‚•§·µñ B‚ÇÇ} (Œ± : F ‚ü∂ G) (Œ≤ : G ‚ü∂ H) (X : B) :
  (Œ± ‚â´ Œ≤).app X = Œ±.app X ‚â´ Œ≤.app X := rfl

@[simp]
lemma comp_app‚ÇÇ {H : B ‚•§·µñ B‚ÇÇ} (Œ± : F ‚ü∂ G) (Œ≤ : G ‚ü∂ H) {X Y : B} (f : X ‚ü∂ Y) :
  (Œ± ‚â´ Œ≤).app‚ÇÇ f = (Œ±_ _ _ _).symm ‚â™‚â´ whiskerRightIso (Œ±.app‚ÇÇ f) (Œ≤.app Y) ‚â™‚â´
    (Œ±_ _ _ _) ‚â™‚â´ whiskerLeftIso _ (Œ≤.app‚ÇÇ _) ‚â™‚â´ (Œ±_ _ _ _).symm := rfl

@[simp]
lemma comp_app‚ÇÇ_hom {H : B ‚•§·µñ B‚ÇÇ} (Œ± : F ‚ü∂ G) (Œ≤ : G ‚ü∂ H) {X Y : B} (f : X ‚ü∂ Y) :
  ((Œ± ‚â´ Œ≤).app‚ÇÇ f).hom = (Œ±_ _ _ _).inv ‚â´ (Œ±.app‚ÇÇ f).hom ‚ñ∑ (Œ≤.app Y) ‚â´
    (Œ±_ _ _ _).hom ‚â´ _ ‚óÅ (Œ≤.app‚ÇÇ f).hom ‚â´ (Œ±_ _ _ _).inv := rfl

@[simp]
lemma comp_app‚ÇÇ_inv {H : B ‚•§·µñ B‚ÇÇ} (Œ± : F ‚ü∂ G) (Œ≤ : G ‚ü∂ H) {X Y : B} (f : X ‚ü∂ Y) :
  ((Œ± ‚â´ Œ≤).app‚ÇÇ f).inv = (Œ±_ _ _ _).hom ‚â´ _ ‚óÅ (Œ≤.app‚ÇÇ f).inv ‚â´
    (Œ±_ _ _ _).inv ‚â´ (Œ±.app‚ÇÇ f).inv ‚ñ∑ (Œ≤.app Y) ‚â´ (Œ±_ _ _ _).hom := by
  dsimp
  simp only [Category.assoc]

variable (F) in
@[simp]
lemma id_app (X : B) : (ùüô F : F ‚ü∂ F).app X = ùüô (F.obj X) := rfl

@[simp]
lemma id_app‚ÇÇ {X Y : B} (f : X ‚ü∂ Y) :
    (ùüô F : F ‚ü∂ F).app‚ÇÇ f = (œÅ_ (F.map f)) ‚â™‚â´ (Œª_ (F.map f)).symm := rfl

@[simp]
lemma id_app‚ÇÇ_hom {X Y : B} (f : X ‚ü∂ Y) :
    ((ùüô F : F ‚ü∂ F).app‚ÇÇ f).hom = (œÅ_ (F.map f)).hom ‚â´ (Œª_ (F.map f)).inv := rfl

@[simp]
lemma id_app‚ÇÇ_inv {X Y : B} (f : X ‚ü∂ Y) :
    ((ùüô F : F ‚ü∂ F).app‚ÇÇ f).inv = (Œª_ (F.map f)).hom ‚â´ (œÅ_ (F.map f)).inv := rfl


@[simp]
lemma whiskerLeft_mod {H : B ‚•§·µñ B‚ÇÇ} (Œ≥ : H ‚ü∂ F) (a : Œ± ‚ü∂ Œ≤) (X : B) :
  (Œ≥ ‚óÅ a).mod X = Œ≥.app X ‚óÅ a.mod X := rfl

@[simp]
lemma whiskerRight_mod {H : B ‚•§·µñ B‚ÇÇ} (a : Œ± ‚ü∂ Œ≤) (Œ≥ : G ‚ü∂ H) (X : B) :
  (a ‚ñ∑ Œ≥).mod X = a.mod X ‚ñ∑ Œ≥.app X := rfl

@[simp]
lemma associator_mod {F G H I : B ‚•§·µñ B‚ÇÇ} (Œ± : F ‚ü∂ G) (Œ≤ : G ‚ü∂ H) (Œ≥ : H ‚ü∂ I) (X : B) :
  (Œ±_ Œ± Œ≤ Œ≥).mod X = Œ±_ (Œ±.app X) (Œ≤.app X) (Œ≥.app X) := rfl

@[simp]
lemma associator_hom_mod {F G H I : B ‚•§·µñ B‚ÇÇ} (Œ± : F ‚ü∂ G) (Œ≤ : G ‚ü∂ H) (Œ≥ : H ‚ü∂ I) (X : B) :
  (Œ±_ Œ± Œ≤ Œ≥).hom.mod X = (Œ±_ (Œ±.app X) (Œ≤.app X) (Œ≥.app X)).hom := rfl

@[simp]
lemma associator_inv_mod {F G H I : B ‚•§·µñ B‚ÇÇ} (Œ± : F ‚ü∂ G) (Œ≤ : G ‚ü∂ H) (Œ≥ : H ‚ü∂ I) (X : B) :
  (Œ±_ Œ± Œ≤ Œ≥).inv.mod X = (Œ±_ (Œ±.app X) (Œ≤.app X) (Œ≥.app X)).inv := rfl

variable (Œ±) in
@[simp]
lemma leftUnitor_mod (X : B) : (Œª_ Œ±).mod X = Œª_ (Œ±.app X) := rfl

variable (Œ±) in
@[simp]
lemma leftUnitor_hom_mod (X : B) : (Œª_ Œ±).hom.mod X = (Œª_ (Œ±.app X)).hom := rfl

variable (Œ±) in
@[simp]
lemma leftUnitor_inv_mod (X : B) : (Œª_ Œ±).inv.mod X = (Œª_ (Œ±.app X)).inv := rfl

variable (Œ±) in
@[simp]
lemma rightUnitor_mod (X : B) : (œÅ_ Œ±).mod X = œÅ_ (Œ±.app X) := rfl

variable (Œ±) in
@[simp]
lemma rightUnitor_hom_mod (X : B) : (œÅ_ Œ±).hom.mod X = (œÅ_ (Œ±.app X)).hom := rfl

variable (Œ±) in
@[simp]
lemma rightUnitor_inv_mod (X : B) : (œÅ_ Œ±).inv.mod X = (œÅ_ (Œ±.app X)).inv := rfl

instance _root_.Pseudofunctor.instBicategoryStrict [Bicategory.Strict B‚ÇÇ] :
  Bicategory.Strict (B ‚•§·µñ B‚ÇÇ) where
    id_comp := by
      intros
      fapply PseudoNatTrans.ext!
      ¬∑ intro X
        simp
      ¬∑ intro X Y f
        simp only [comp_app, id_app, comp_app‚ÇÇ, id_app‚ÇÇ, Iso.trans_hom, Iso.symm_hom,
          whiskerRightIso_hom, comp_whiskerRight, leftUnitor_inv_whiskerRight, whiskerLeftIso_hom,
          id_whiskerLeft, Category.assoc, Iso.inv_hom_id_assoc, triangle_assoc_comp_right_assoc]
        simp [Strict.leftUnitor_eqToIso, Strict.associator_eqToIso]
    comp_id := by
      intros
      fapply PseudoNatTrans.ext!
      ¬∑ intro X
        simp
      ¬∑ intros
        simp only [comp_app, id_app, comp_app‚ÇÇ, id_app‚ÇÇ, Iso.trans_hom, Iso.symm_hom,
          whiskerRightIso_hom, whiskerRight_id, whiskerLeftIso_hom, whiskerLeft_comp,
          whiskerLeft_rightUnitor, Category.assoc, triangle_assoc_comp_left_inv,
          Iso.hom_inv_id_assoc, Iso.inv_hom_id_assoc]
        simp [Strict.rightUnitor_eqToIso, Strict.associator_eqToIso]
    assoc f g h := by
      -- simp [(¬∑ ‚â´ ¬∑)]
      apply PseudoNatTrans.ext!
      ¬∑ simp [Strict.associator_eqToIso]
      ¬∑ simp
    leftUnitor_eqToIso := by
      intro F G Œ±
      ext X
      simp [Strict.leftUnitor_eqToIso]
    rightUnitor_eqToIso := by
      intros
      ext X
      simp [Strict.rightUnitor_eqToIso]
    associator_eqToIso := by
      intros
      ext X
      simp [Strict.associator_eqToIso]

end PseudoNatTrans

end PseudoNatTrans
end Bicategory
section Cat
open Bicategory

def NatTrans.toCatHom‚ÇÇ {C : Type u} [Category.{v} C] {D : Type u} [Category.{v} D]
  {F G : C ‚•§ D} (Œ∑ : F ‚ü∂ G) : (F.toCatHom : Cat.of C ‚ü∂ Cat.of D) ‚ü∂ G.toCatHom :=
  Œ∑

def NatTrans.ofCatHom‚ÇÇ {C : Type u} [Category.{v} C] {D : Type u} [Category.{v} D]
  {F G : C ‚•§ D} (Œ∑ : F.toCatHom ‚ü∂ G.toCatHom) :
  F ‚ü∂ G := Œ∑

@[simp]
lemma NatTrans.ofCatHom‚ÇÇ_toCatHom‚ÇÇ {C : Type u} [Category.{v} C] {D : Type u} [Category.{v} D]
  {F G : C ‚•§ D} (Œ∑ : F ‚ü∂ G) : NatTrans.ofCatHom‚ÇÇ Œ∑.toCatHom‚ÇÇ = Œ∑ := rfl


@[ext]
lemma ext {C : Type u} [Category.{v} C] {D : Type u} [Category.{v} D]
  {F G : C ‚•§ D} (Œ∑‚ÇÅ Œ∑‚ÇÇ : F.toCatHom ‚ü∂ G.toCatHom)
    (h : NatTrans.ofCatHom‚ÇÇ Œ∑‚ÇÅ = NatTrans.ofCatHom‚ÇÇ Œ∑‚ÇÇ) : Œ∑‚ÇÅ = Œ∑‚ÇÇ := h

@[simp]
lemma NatTrans.ofCatHom‚ÇÇ_id {C : Type u} [Category.{v} C] {D : Type u} [Category.{v} D]
  (F : C ‚•§ D) : NatTrans.ofCatHom‚ÇÇ (ùüô F.toCatHom) = ùüô F := rfl

@[simp]
lemma NatTrans.ofCatHom‚ÇÇ_comp {C : Type u} [Category.{v} C] {D : Type u} [Category.{v} D]
  {F G H : C ‚•§ D} (Œ∑‚ÇÅ : F.toCatHom ‚ü∂ G.toCatHom) (Œ∑‚ÇÇ : G.toCatHom ‚ü∂ H.toCatHom) :
    NatTrans.ofCatHom‚ÇÇ (Œ∑‚ÇÅ ‚â´ Œ∑‚ÇÇ) = NatTrans.ofCatHom‚ÇÇ Œ∑‚ÇÅ ‚â´ NatTrans.ofCatHom‚ÇÇ Œ∑‚ÇÇ := rfl

@[simp]
lemma NatTrans.ofCatHom‚ÇÇ_whiskerLeft {C : Type u} [Category.{v} C] {D : Type u} [Category.{v} D]
    {E : Type u} [Category.{v} E] (F : C ‚•§ D) {G H : D ‚•§ E} (Œ∑ : G.toCatHom ‚ü∂ H.toCatHom) :
    NatTrans.ofCatHom‚ÇÇ (F.toCatHom ‚óÅ Œ∑) = F.whiskerLeft (NatTrans.ofCatHom‚ÇÇ Œ∑) := rfl

@[simp]
lemma NatTrans.ofCatHom‚ÇÇ_associator_hom {A C D E : Type u} [Category.{v} A]
  [Category.{v} C] [Category.{v} D] [Category.{v} E] (F : A ‚•§ C) (G : C ‚•§ D) (H : D ‚•§ E) :
  NatTrans.ofCatHom‚ÇÇ (Œ±_ F.toCatHom G.toCatHom H.toCatHom).hom =
    (Functor.associator F G H).hom := rfl

@[simp]
lemma NatTrans.ofCatHom‚ÇÇ_associator_inv {A C D E : Type u} [Category.{v} A]
  [Category.{v} C] [Category.{v} D] [Category.{v} E] (F : A ‚•§ C) (G : C ‚•§ D) (H : D ‚•§ E) :
  NatTrans.ofCatHom‚ÇÇ (Œ±_ F.toCatHom G.toCatHom H.toCatHom).inv =
    (Functor.associator F G H).inv := rfl

@[simp]
lemma NatTrans.ofCatHom‚ÇÇ_leftUnitor_hom {C : Type u} [Category.{v} C] {D : Type u} [Category.{v} D]
  (F : C ‚•§ D) : NatTrans.ofCatHom‚ÇÇ (Œª_ F.toCatHom).hom = (Functor.leftUnitor F).hom := rfl

@[simp]
lemma NatTrans.ofCatHom‚ÇÇ_leftUnitor_inv {C : Type u} [Category.{v} C] {D : Type u} [Category.{v} D]
  (F : C ‚•§ D) : NatTrans.ofCatHom‚ÇÇ (Œª_ F.toCatHom).inv = (Functor.leftUnitor F).inv := rfl

@[simp]
lemma NatTrans.ofCatHom‚ÇÇ_rightUnitor_hom {C : Type u} [Category.{v} C] {D : Type u} [Category.{v} D]
  (F : C ‚•§ D) : NatTrans.ofCatHom‚ÇÇ (œÅ_ F.toCatHom).hom = (Functor.rightUnitor F).hom := rfl

@[simp]
lemma NatTrans.ofCatHom‚ÇÇ_rightUnitor_inv {C : Type u} [Category.{v} C] {D : Type u} [Category.{v} D]
  (F : C ‚•§ D) : NatTrans.ofCatHom‚ÇÇ (œÅ_ F.toCatHom).inv = (Functor.rightUnitor F).inv := rfl

-- @[simp]
-- lemma NatTrans.ofCatHom‚ÇÇ_associator_inv {A C D E : Type u} [Category.{v} A]
--   [Category.{v} C] [Category.{v} D] [Category.{v} E] (F : A ‚•§ C) (G : C ‚•§ D) (H : D ‚•§ E) :
--   NatTrans.ofCatHom‚ÇÇ (Œ±_ F.toCatHom G.toCatHom H.toCatHom).inv =
--     (Functor.associator F G H).inv := rfl



-- example {C : Type u} [Category.{v} C] {D : Type u} [Category.{v} D]
--     {E : Type u} [Category.{v} E] (F : C ‚•§ D) {G H : D ‚•§ E} (Œ∑ : G.toCatHom ‚ü∂ H.toCatHom) :
--     True := by
--   have := NatTrans.ofCatHom‚ÇÇ_whiskerLeft F Œ∑
--   simp only [Cat.of_Œ±] at this

@[simp]
lemma NatTrans.ofCatHom‚ÇÇ_whiskerRight {C : Type u} [Category.{v} C] {D : Type u} [Category.{v} D]
    {E : Type u} [Category.{v} E] {F G : C ‚•§ D} (Œ∑ : F.toCatHom ‚ü∂ G.toCatHom) (H : D ‚•§ E) :
    NatTrans.ofCatHom‚ÇÇ (Œ∑ ‚ñ∑ H.toCatHom) = Functor.whiskerRight (NatTrans.ofCatHom‚ÇÇ Œ∑) H := rfl

-- @[simp]
-- lemma NatTrans.toCatHom‚ÇÇ_whiskerLeft {C : Type u} [Category.{v} C] {D : Type u} [Category.{v} D]
--     {E : Type u} [Category.{v} E] (F : C ‚•§ D) {G H : D ‚•§ E} (Œ∑ : G ‚ü∂ H) :
--     NatTrans.toCatHom‚ÇÇ (Functor.whiskerLeft F Œ∑) = F.toCatHom ‚óÅ (NatTrans.toCatHom‚ÇÇ Œ∑) := rfl

@[simps]
def NatIso.toCatIso‚ÇÇ {C : Type u} [Category.{v} C] {D : Type u} [Category.{v} D]
    {F G : C ‚•§ D} (Œ∑ : F ‚âÖ G) : F.toCatHom ‚âÖ G.toCatHom where
  hom := Œ∑.hom.toCatHom‚ÇÇ
  inv := Œ∑.inv.toCatHom‚ÇÇ
  hom_inv_id := by ext; simp
  inv_hom_id := by ext; simp

@[simps]
def NatIso.ofCatIso‚ÇÇ {C : Type u} [Category.{v} C] {D : Type u} [Category.{v} D]
    {F G : C ‚•§ D} (Œ∑ : F.toCatHom ‚âÖ G.toCatHom) : F ‚âÖ G where
  hom := NatTrans.ofCatHom‚ÇÇ Œ∑.hom
  inv := NatTrans.ofCatHom‚ÇÇ Œ∑.inv
  hom_inv_id := by rw [‚Üê NatTrans.ofCatHom‚ÇÇ_comp]; simp
  inv_hom_id := by rw [‚Üê NatTrans.ofCatHom‚ÇÇ_comp]; simp

-- lemma NatIso.ofCatIso‚ÇÇ_whiskerLeft {C : Type u} [Category.{v} C] {D : Type u} [Category.{v} D]
--     {E : Type u} [Category.{v} E] (F : C ‚•§ D) {G H : D ‚•§ E} (Œ∑ : G.toCatHom) :
--     NatIso.ofCatIso‚ÇÇ (F.toCatHom ‚óÅ _) = whiskerLeft _ := rfl

end Cat


namespace Pseudofunctor
open Bicategory
variable {B}
variable {B‚ÇÇ : Type u‚ÇÇ} [Bicategory.{w‚ÇÇ, v‚ÇÇ} B‚ÇÇ] {B‚ÇÉ : Type u‚ÇÉ} [Bicategory.{w‚ÇÉ, v‚ÇÉ} B‚ÇÉ]
section flip

-- we don't use @[simps] here because it generates annoyingly long lemma names.
def flipObj (F : B ‚•§·µñ B‚ÇÇ ‚•§·µñ B‚ÇÉ) (Y : B‚ÇÇ) : B ‚•§·µñ B‚ÇÉ where
  obj X := (F.obj X).obj Y
  map {X‚ÇÅ X‚ÇÇ} f := (F.map f).app Y
  map‚ÇÇ {X‚ÇÅ X‚ÇÇ} {f‚ÇÅ f‚ÇÇ} Œ∑ := (F.map‚ÇÇ Œ∑).mod Y
  map‚ÇÇ_id {X‚ÇÅ X‚ÇÇ} f := by simp
  map‚ÇÇ_comp {X‚ÇÅ X‚ÇÇ} {f‚ÇÅ f‚ÇÇ f‚ÇÉ} Œ∑ Œ∏ := by simp
  mapId X := (F.mapId X).mod Y
  mapComp {X‚ÇÅ X‚ÇÇ X‚ÇÉ} f‚ÇÅ f‚ÇÇ := (F.mapComp f‚ÇÅ f‚ÇÇ).mod Y
  map‚ÇÇ_whisker_left {X‚ÇÅ X‚ÇÇ X‚ÇÉ} f {g‚ÇÅ g‚ÇÇ} Œ∑ := by
    simp only [Pseudofunctor.map‚ÇÇ_whisker_left, PseudoModification.comp_mod_app, Iso.mod_hom,
      Iso.mod_inv]
    rfl
  map‚ÇÇ_whisker_right {X‚ÇÅ X‚ÇÇ X‚ÇÉ} {f‚ÇÅ f‚ÇÇ} Œ∑ g := by simp
  map‚ÇÇ_associator := by intros; simp
  map‚ÇÇ_left_unitor := by intros; simp
  map‚ÇÇ_right_unitor := by intros; simp

@[simp]
lemma flipObj_obj (F : B ‚•§·µñ B‚ÇÇ ‚•§·µñ B‚ÇÉ) (Y : B‚ÇÇ) (X : B) :
  (F.flipObj Y).obj X = (F.obj X).obj Y := rfl

@[simp]
lemma flipObj_map (F : B ‚•§·µñ B‚ÇÇ ‚•§·µñ B‚ÇÉ) (Y : B‚ÇÇ) {X‚ÇÅ X‚ÇÇ : B} (f : X‚ÇÅ ‚ü∂ X‚ÇÇ) :
  (F.flipObj Y).map f = (F.map f).app Y := rfl

@[simp]
lemma flipObj_map‚ÇÇ (F : B ‚•§·µñ B‚ÇÇ ‚•§·µñ B‚ÇÉ) (Y : B‚ÇÇ) {X‚ÇÅ X‚ÇÇ : B} {f‚ÇÅ f‚ÇÇ : X‚ÇÅ ‚ü∂ X‚ÇÇ} (Œ∑ : f‚ÇÅ ‚ü∂ f‚ÇÇ) :
  (F.flipObj Y).map‚ÇÇ Œ∑ = (F.map‚ÇÇ Œ∑).mod Y := rfl

@[simp]
lemma flipObj_mapId (F : B ‚•§·µñ B‚ÇÇ ‚•§·µñ B‚ÇÉ) (Y : B‚ÇÇ) (X : B) : (F.flipObj Y).mapId X =
  (F.mapId X).mod Y := rfl

@[simp]
lemma flipObj_mapComp (F : B ‚•§·µñ B‚ÇÇ ‚•§·µñ B‚ÇÉ) (Y : B‚ÇÇ) {X‚ÇÅ X‚ÇÇ X‚ÇÉ : B} (f‚ÇÅ : X‚ÇÅ ‚ü∂ X‚ÇÇ) (f‚ÇÇ : X‚ÇÇ ‚ü∂ X‚ÇÉ) :
  (F.flipObj Y).mapComp f‚ÇÅ f‚ÇÇ = (F.mapComp f‚ÇÅ f‚ÇÇ).mod Y := rfl

@[simps]
def flipMap (F : B ‚•§·µñ B‚ÇÇ ‚•§·µñ B‚ÇÉ) {Y‚ÇÅ Y‚ÇÇ : B‚ÇÇ} (g : Y‚ÇÅ ‚ü∂ Y‚ÇÇ) : flipObj F Y‚ÇÅ ‚ü∂ flipObj F Y‚ÇÇ where
  app X := (F.obj X).map g
  app‚ÇÇ {X‚ÇÅ X‚ÇÇ} f := ((F.map f).app‚ÇÇ g).symm
  app‚ÇÇ_naturality {X‚ÇÅ X‚ÇÇ} {f‚ÇÅ f‚ÇÇ} Œ∑ := (F.map‚ÇÇ Œ∑).mod_naturality g
  unitality X := by
    dsimp only [flipObj_obj, flipObj_map, flipObj_mapId, Iso.mod_inv, Iso.symm_inv]
    have := (F.mapId X).mod_inv_naturality g
    rw [Iso.comp_inv_eq, Iso.mod_inv] at this
    rw [this]
    simp
  associativity {X‚ÇÅ X‚ÇÇ X‚ÇÉ} f‚ÇÅ f‚ÇÇ := by
    dsimp only [flipObj_obj, flipObj_map, Iso.symm_inv, flipObj_mapComp, Iso.mod_inv]
    have := (F.mapComp f‚ÇÅ f‚ÇÇ).mod_inv_naturality g
    rw [Iso.comp_inv_eq, Iso.mod_inv] at this
    rw [this]
    simp

@[simps]
def flipMap‚ÇÇ (F : B ‚•§·µñ B‚ÇÇ ‚•§·µñ B‚ÇÉ) {Y‚ÇÅ Y‚ÇÇ : B‚ÇÇ} {g‚ÇÅ g‚ÇÇ : Y‚ÇÅ ‚ü∂ Y‚ÇÇ} (Œ∑ : g‚ÇÅ ‚ü∂ g‚ÇÇ) :
    flipMap F g‚ÇÅ ‚ü∂ flipMap F g‚ÇÇ where
  mod X := (F.obj X).map‚ÇÇ Œ∑
  mod_naturality {_X‚ÇÅ _X‚ÇÇ} f := (F.map f).app‚ÇÇ_naturality Œ∑

-- again, no @[simps].
def flip (F : B ‚•§·µñ B‚ÇÇ ‚•§·µñ B‚ÇÉ) : B‚ÇÇ ‚•§·µñ B ‚•§·µñ B‚ÇÉ where
  obj Y := flipObj F Y
  map {Y‚ÇÅ Y‚ÇÇ} g := flipMap F g
  map‚ÇÇ {Y‚ÇÅ Y‚ÇÇ} {g‚ÇÅ g‚ÇÇ} Œ∑ := flipMap‚ÇÇ F Œ∑
  map‚ÇÇ_id {Y‚ÇÅ Y‚ÇÇ} g:= by
    ext X
    exact (F.obj X).map‚ÇÇ_id g
  map‚ÇÇ_comp {Y‚ÇÅ Y‚ÇÇ} {g‚ÇÅ g‚ÇÇ g‚ÇÉ} h‚ÇÅ h‚ÇÇ := by
    ext X
    exact (F.obj X).map‚ÇÇ_comp h‚ÇÅ h‚ÇÇ
  mapId Y := PseudoNatTrans.ext (fun X => (F.obj X).mapId Y) (by
    dsimp only [flipObj_obj, flipMap_app, flipObj_map, PseudoNatTrans.id_app,
      PseudoNatTrans.id_app‚ÇÇ, Iso.trans_inv, Iso.symm_inv, flipMap_app‚ÇÇ]
    intro X‚ÇÅ X‚ÇÇ f
    have := (F.map f).unitality Y
    rw [Iso.eq_comp_inv,Category.assoc,‚Üê Iso.eq_inv_comp,‚Üê whiskerLeftIso_inv,
      Iso.eq_comp_inv,whiskerLeftIso_hom] at this
    rw [‚Üê this]
    simp
  )
  mapComp {Y‚ÇÅ Y‚ÇÇ Y‚ÇÉ} g‚ÇÅ g‚ÇÇ := PseudoNatTrans.ext (fun X => (F.obj X).mapComp g‚ÇÅ g‚ÇÇ) (by
    dsimp only [flipObj_obj, flipMap_app, flipObj_map, PseudoNatTrans.comp_app,
      PseudoNatTrans.comp_app‚ÇÇ, flipMap_app‚ÇÇ, Iso.trans_inv, Iso.symm_inv, whiskerLeftIso_inv,
      whiskerRightIso_inv]
    intro X‚ÇÅ X‚ÇÇ f
    have := (F.map f).associativity g‚ÇÅ g‚ÇÇ
    simp_rw [‚Üê whiskerLeftIso_inv,‚Üê whiskerRightIso_inv] at this
    rw [‚Üê Iso.inv_comp_eq,Iso.eq_inv_comp] at this
    rw [‚Üê Iso.inv_comp_eq, ‚Üê this]
    simp
    )
  map‚ÇÇ_whisker_left := by intros; ext X; simp
  map‚ÇÇ_whisker_right := by intros; ext X; simp
  map‚ÇÇ_associator := by intros; ext X; simp
  map‚ÇÇ_left_unitor := by intros; ext X; simp
  map‚ÇÇ_right_unitor := by intros; ext X; simp

@[simp]
lemma flip_obj (F : B ‚•§·µñ B‚ÇÇ ‚•§·µñ B‚ÇÉ) (Y : B‚ÇÇ) : F.flip.obj Y = (F.flipObj Y) := rfl

@[simp]
lemma flip_map (F : B ‚•§·µñ B‚ÇÇ ‚•§·µñ B‚ÇÉ) {Y‚ÇÅ Y‚ÇÇ : B‚ÇÇ} (g : Y‚ÇÅ ‚ü∂ Y‚ÇÇ) :
    F.flip.map g = F.flipMap g := rfl

@[simp]
lemma flip_map‚ÇÇ (F : B ‚•§·µñ B‚ÇÇ ‚•§·µñ B‚ÇÉ) {Y‚ÇÅ Y‚ÇÇ : B‚ÇÇ} {g‚ÇÅ g‚ÇÇ : Y‚ÇÅ ‚ü∂ Y‚ÇÇ} (Œ∑ : g‚ÇÅ ‚ü∂ g‚ÇÇ) :
  F.flip.map‚ÇÇ Œ∑ = F.flipMap‚ÇÇ Œ∑ := rfl

@[simp]
lemma flip_mapId (F : B ‚•§·µñ B‚ÇÇ ‚•§·µñ B‚ÇÉ) (Y : B‚ÇÇ) :
  F.flip.mapId Y = PseudoNatTrans.ext (fun X => (F.obj X).mapId Y) (flip._proof_3 F Y) := rfl

@[simp]
lemma flip_mapComp (F : B ‚•§·µñ B‚ÇÇ ‚•§·µñ B‚ÇÉ) {Y‚ÇÅ Y‚ÇÇ Y‚ÇÉ : B‚ÇÇ} (g‚ÇÅ : Y‚ÇÅ ‚ü∂ Y‚ÇÇ) (g‚ÇÇ : Y‚ÇÇ ‚ü∂ Y‚ÇÉ) :
    F.flip.mapComp g‚ÇÅ g‚ÇÇ = PseudoNatTrans.ext (fun X => (F.obj X).mapComp g‚ÇÅ g‚ÇÇ)
    (flip._proof_4 F g‚ÇÅ g‚ÇÇ) := rfl

@[simp]
lemma flip_flip (F : B ‚•§·µñ B‚ÇÇ ‚•§·µñ B‚ÇÉ) : F.flip.flip = F := by -- actually rfl, but that takes long.
  cases F
  rw [flip, flip]
  congr

end flip
end Pseudofunctor

section coyoneda

open Bicategory
variable {B}

def Bicategory.pseudoCoyonedaObj (X : B·µí·µñ) : B ‚•§·µñ Cat where
  obj Y := .of (X.unop ‚ü∂ Y)
  map {Y‚ÇÅ Y‚ÇÇ} f‚ÇÅ := Functor.toCatHom ((Bicategory.postcomp X.unop f‚ÇÅ))
  map‚ÇÇ {Y‚ÇÅ Y‚ÇÇ} {f‚ÇÅ f‚ÇÇ} Œ∑ :=
    NatTrans.toCatHom‚ÇÇ ((Bicategory.postcomposing X.unop Y‚ÇÅ Y‚ÇÇ).map Œ∑)
  map‚ÇÇ_id {Y‚ÇÅ Y‚ÇÇ} f := by
    ext f‚ÇÇ
    simp
  map‚ÇÇ_comp {Y‚ÇÅ Y‚ÇÇ} {f‚ÇÅ f‚ÇÇ f‚ÇÉ} Œ∑‚ÇÅ Œ∑‚ÇÇ := by
    ext f‚ÇÑ
    simp [NatTrans.comp_app _ _]
  mapId Y := NatIso.toCatIso‚ÇÇ <| Bicategory.rightUnitorNatIso X.unop Y
  mapComp {Y‚ÇÅ Y‚ÇÇ Y‚ÇÉ} f‚ÇÅ f‚ÇÇ := NatIso.toCatIso‚ÇÇ (Bicategory.associatorNatIsoLeft X.unop f‚ÇÅ f‚ÇÇ).symm
  map‚ÇÇ_whisker_left := by intros; ext f‚ÇÇ; simp [NatTrans.ofCatHom‚ÇÇ_whiskerLeft _ _]
  map‚ÇÇ_whisker_right := by intros; ext f‚ÇÇ; simp [NatTrans.ofCatHom‚ÇÇ_whiskerRight _ _]
  map‚ÇÇ_associator := by intros; ext; simp [NatTrans.ofCatHom‚ÇÇ_toCatHom‚ÇÇ, NatIso.toCatIso‚ÇÇ_hom,
      NatTrans.ofCatHom‚ÇÇ_whiskerRight _ _, NatTrans.ofCatHom‚ÇÇ_associator_hom _ _ _,
      NatTrans.ofCatHom‚ÇÇ_whiskerLeft _ _, Category.id_comp ((Œ±_ _ _ _).hom ‚â´ _)]
  map‚ÇÇ_left_unitor := by intros; ext f‚ÇÇ; simp [NatTrans.ofCatHom‚ÇÇ_whiskerRight _ _,
    NatTrans.ofCatHom‚ÇÇ_leftUnitor_hom _]
  map‚ÇÇ_right_unitor := by intros; ext f‚ÇÇ; simp [NatTrans.ofCatHom‚ÇÇ_whiskerLeft _ _,
    NatTrans.ofCatHom‚ÇÇ_rightUnitor_hom _, Category.comp_id (œÅ_ (f‚ÇÇ ‚â´ _)).hom]

@[simp]
lemma Bicategory.pseudoCoyonedaObj_obj {X : B·µí·µñ} (Y : B) :
  (pseudoCoyonedaObj X).obj Y = .of (X.unop ‚ü∂ Y) := rfl

@[simp]
lemma Bicategory.pseudoCoyonedaObj_map {X : B·µí·µñ} {Y‚ÇÅ Y‚ÇÇ : B} (g : Y‚ÇÅ ‚ü∂ Y‚ÇÇ) :
  (pseudoCoyonedaObj X).map g = (Bicategory.postcomp X.unop g).toCatHom := rfl

@[simp]
lemma Bicategory.pseudoCoyonedaObj_map2 {X : B·µí·µñ} {Y‚ÇÅ Y‚ÇÇ : B} {g‚ÇÅ g‚ÇÇ : Y‚ÇÅ ‚ü∂ Y‚ÇÇ}
    (Œ∑ : g‚ÇÅ ‚ü∂ g‚ÇÇ) : (pseudoCoyonedaObj X).map‚ÇÇ Œ∑ =
      NatTrans.toCatHom‚ÇÇ ((Bicategory.postcomposing X.unop Y‚ÇÅ Y‚ÇÇ).map Œ∑) := rfl


@[simps]
def Bicategory.pseudoCoyonedaMap {X‚ÇÅ X‚ÇÇ : B·µí·µñ} (f : X‚ÇÅ ‚ü∂ X‚ÇÇ) :
    pseudoCoyonedaObj X‚ÇÅ ‚ü∂ pseudoCoyonedaObj X‚ÇÇ where
  app Y := {
    obj f‚ÇÅ := f.unop ‚â´ f‚ÇÅ
    map {f‚ÇÅ f‚ÇÇ} Œ∑ := f.unop ‚óÅ Œ∑
    map_id f‚ÇÅ := whiskerLeft_id f.unop f‚ÇÅ
    map_comp {f‚ÇÅ f‚ÇÇ f‚ÇÉ} Œ∑‚ÇÅ Œ∑‚ÇÇ := whiskerLeft_comp f.unop Œ∑‚ÇÅ Œ∑‚ÇÇ
  }
  app‚ÇÇ {Y‚ÇÅ Y‚ÇÇ} f‚ÇÇ := NatIso.ofComponents (fun f‚ÇÉ => (Œ±_ f.unop f‚ÇÉ f‚ÇÇ).symm) (by intros; simp)
  app‚ÇÇ_naturality {X‚ÇÅ X‚ÇÇ} {f‚ÇÅ f‚ÇÇ} Œ∑ := by ext f‚ÇÉ; simp
  unitality X := by
    ext f‚ÇÉ
    simp_rw [Cat.comp_app, Cat.rightUnitor_hom_app, Cat.leftUnitor_inv_app, Cat.whiskerLeft_app]
    simp
  associativity {X‚ÇÅ X‚ÇÇ X‚ÇÉ} f‚ÇÅ f‚ÇÇ := by
    ext f‚ÇÉ
    simp_rw [Cat.comp_app, Cat.associator_hom_app,Cat.associator_inv_app]
    simp

@[simps]
def Bicategory.pseudoCoyonedaMap‚ÇÇ {X‚ÇÅ X‚ÇÇ : B·µí·µñ} {f‚ÇÅ f‚ÇÇ : X‚ÇÅ ‚ü∂ X‚ÇÇ} (Œ∑ : f‚ÇÅ ‚ü∂ f‚ÇÇ) :
    pseudoCoyonedaMap f‚ÇÅ ‚ü∂ pseudoCoyonedaMap f‚ÇÇ where
  mod Y := {
    app f‚ÇÉ := Œ∑.unop2 ‚ñ∑ f‚ÇÉ
    naturality {f‚ÇÉ f‚ÇÑ} Œ∑‚ÇÇ := whisker_exchange Œ∑.unop2 Œ∑‚ÇÇ
  }
  mod_naturality {Y‚ÇÅ Y‚ÇÇ} g := by ext g‚ÇÇ; simp

@[simps]
def Bicategory.pseudoCoyoneda : B·µí·µñ ‚•§·µñ B ‚•§·µñ Cat where
  obj X := pseudoCoyonedaObj X
  map {X‚ÇÅ X‚ÇÇ} f := pseudoCoyonedaMap f
  map‚ÇÇ {X‚ÇÅ X‚ÇÇ} {f‚ÇÅ f‚ÇÇ} Œ∑ := pseudoCoyonedaMap‚ÇÇ Œ∑
  map‚ÇÇ_id {X‚ÇÅ X‚ÇÇ} f:= by ext Y g; simp
  map‚ÇÇ_comp {X‚ÇÅ X‚ÇÇ} {f‚ÇÅ f‚ÇÇ f‚ÇÉ} Œ∑ Œ∑‚ÇÇ := by ext Y g; simp
  mapId X := PseudoNatTrans.ext (fun Y => NatIso.ofComponents (fun g => Œª_ g) (by intros; simp)) (by
    intros
    ext g
    simp [Cat.comp_app,Cat.rightUnitor_inv_app,Cat.leftUnitor_hom_app])
  mapComp {X‚ÇÅ X‚ÇÇ X‚ÇÇ} f‚ÇÅ f‚ÇÇ := PseudoNatTrans.ext
    (fun Y => NatIso.ofComponents (fun g => Œ±_ f‚ÇÇ.unop f‚ÇÅ.unop g)) (by
    intros
    ext g
    simp [Cat.comp_app, Cat.associator_hom_app,Cat.associator_inv_app]
    )
  map‚ÇÇ_whisker_left := by intros; ext X f; simp
  map‚ÇÇ_whisker_right := by intros; ext X f; simp
  map‚ÇÇ_associator := by intros; ext X f; simp [Cat.comp_app, Cat.associator_hom_app]
  map‚ÇÇ_left_unitor := by intros; ext X f; simp [Cat.comp_app, Cat.leftUnitor_hom_app]
  map‚ÇÇ_right_unitor := by intros; ext X f; simp [Cat.comp_app, Cat.rightUnitor_hom_app]

end coyoneda


end CategoryTheory
