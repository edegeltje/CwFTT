import Mathlib.CategoryTheory.Subobject.Basic
import Mathlib.Order.Category.Preord
import Mathlib.CategoryTheory.Bicategory.Strict.Basic
import Mathlib.CategoryTheory.Bicategory.Functor.StrictPseudofunctor
import Mathlib.CategoryTheory.Bicategory.LocallyDiscrete
import Mathlib.CategoryTheory.Whiskering
import CwFTT.Util.Pullback
/-!
Weewoo a docstring

### Partial Maps
This file defines *partial map diagrams* and *partial maps* in categories. It defines the partial
order of partial maps from `X` to `Y` as well as the category of partial map diagrams from `X` to
`Y`.

## Short explanation

A partial map (in usual parlance, i.e. say set theory) from `X` to `Y` is a function from some
subset of X to Y. In category theory, when interpreting morphisms as functions between sets/types,
this corresponds to an object `U` and two morphisms `m : U ‚ü∂ X` and `f : U ‚ü∂ Y` with `m` mono
(making `U` a literal subobject of `X`, and `f` the function from the subobject to `Y`).
Similar to how `Subobject`s of `X` are monomorphisms into `X` "up to isomorphism", for partial maps
we only consider the previously mentioned diagrams up to isomorphisms (between the respective
objects `U`) which transform the respective maps `m` and `f` into eachother.

## Definitions
- `ObjectProperty.IsPartialMap X Y` is the predicate on objects in `BinaryFan X Y` satisfied by
  fans where the projection to `X` is mono.
- `PrePartialMap X Y` is the category of partial map diagrams with domain `X` and codomain `Y`.
  It is defined as the full subcategory of `BinaryFan X Y` induced by `IsPartialMap X Y`.
  This category is thin (Homsets are subsingleton).
  In the file we also introduce the notation `X ‚áÄ' Y` (typed with \rightharpoonup) for this type.
- `PartialMap X Y` is the partial order of partial maps with domain `X` and codomain `Y`.
  It is defined as the thin skeleton of `PrePartialMap X Y` (i.e. the quotient by iso)
  In the file we also introduce the notation `X ‚áÄ Y` for this type.

- ``


-/
universe v u
namespace CategoryTheory
open Limits
variable {C : Type u} [Category.{v} C]

/-- A (concrete) partial map diagram in a category `C` from `X` to `Y` is a binary fan into `X` and
  `Y` such that the map into `X` is mono. -/
def ObjectProperty.IsPartialMap (X Y : C) : ObjectProperty (Limits.BinaryFan X Y) :=
  (Mono ¬∑.fst)

/-- The category of concrete partial map diagrams in the category `C` with domain `X` and
  codomain `X` -/
abbrev PrePartialMap (X Y : C) := (ObjectProperty.IsPartialMap X Y).FullSubcategory

instance {X Y : C} (c : (ObjectProperty.IsPartialMap X Y).FullSubcategory) :
  Mono (c.obj.fst) := c.property

variable (C) in
structure _root_.CategoryTheory.WithPrePartialMaps where
  mk :: (out : C)

attribute [pp_nodot] WithPrePartialMaps.mk

-- TODO : Modulize, make meta
@[app_unexpander WithPrePartialMaps.mk]
protected def WithPrePartialMaps.unexpander_mk : Lean.PrettyPrinter.Unexpander
  | s => pure s

instance : Quiver (WithPrePartialMaps C) where
  Hom X Y := PrePartialMap X.out Y.out

instance (X Y : WithPrePartialMaps C) : Category (X ‚ü∂ Y) := inferInstanceAs
  (Category (PrePartialMap X.out Y.out))

@[inherit_doc PrePartialMap]
scoped notation:40 x:41 " ‚áÄ' " y:41 =>
  (WithPrePartialMaps.mk x) ‚ü∂ (WithPrePartialMaps.mk y)

/-- The subcategory of partial map diagrams is thin, making it sensible to use `ThinSkeleton` -/
instance {X Y : C} : Quiver.IsThin (X ‚áÄ' Y) := fun
  | .mk obj property => fun b =>
    { allEq f‚ÇÅ f‚ÇÇ := ObjectProperty.hom_ext _ <| ConeMorphism.ext _ _
      <| b.property.right_cancellation _ _ (by simp)
      }

namespace PrePartialMap

/--
create a partial map diagram by providing a monomorphism `m : U ‚ü∂ X` and a morphism `f : U ‚ü∂ Y`
-/
def mk {X Y U : C} (m : U ‚ü∂ X) [Mono m] (f : U ‚ü∂ Y) :
    X ‚áÄ' Y where
  obj := BinaryFan.mk m f
  property := inferInstanceAs (Mono m)

@[simp]
lemma mk_obj {X Y : C} (x : X ‚áÄ' Y) :
    .mk x.obj.fst x.obj.snd = x := by
  refine ObjectProperty.FullSubcategory.ext ?_
  simp only [mk, Functor.const_obj_obj]
  dsimp [BinaryFan.mk]
  congr
  ext j
  match j with
  | .mk .left => simp
  | .mk .right => simp

@[simp]
lemma mk_obj_pt {X Y U : C} (m : U ‚ü∂ X) [Mono m] (f : U ‚ü∂ Y) :
  (mk m f).obj.pt = U := rfl

@[simp]
lemma mk_obj_fst {X Y U : C} (m : U ‚ü∂ X) [Mono m] (f : U ‚ü∂ Y) :
  (mk m f).obj.fst = m := rfl

@[simp]
lemma mk_obj_snd {X Y U : C} (m : U ‚ü∂ X) [Mono m] (f : U ‚ü∂ Y) :
  (mk m f).obj.snd = f := rfl

def mkOfHom {X Y : C} (f : X ‚ü∂ Y) : X ‚áÄ' Y := mk (ùüô X) f

@[simp]
lemma mkOfHom_obj_pt {X Y : C} (f : X ‚ü∂ Y) : (mkOfHom f).obj.pt = X := rfl

@[simp]
lemma mkOfHom_obj_fst {X Y : C} (f : X ‚ü∂ Y) : (mkOfHom f).obj.fst = ùüô X := rfl

@[simp]
lemma mkOfHom_obj_snd {X Y : C} (f : X ‚ü∂ Y) : (mkOfHom f).obj.snd = f := rfl


def mkOfMono {X Y : C} (m : Y ‚ü∂ X) [Mono m] : X ‚áÄ' Y := mk m (ùüô Y)

@[simp]
lemma mkOfMono_obj_pt {X Y : C} (m : Y ‚ü∂ X) [Mono m] : (mkOfMono m).obj.pt = Y := rfl

@[simp]
lemma mkOfMono_obj_fst {X Y : C} (m : Y ‚ü∂ X) [Mono m] : (mkOfMono m).obj.fst = m := rfl

@[simp]
lemma mkOfMono_obj_snd {X Y : C} (m : Y ‚ü∂ X) [Mono m] : (mkOfMono m).obj.snd = ùüô Y := rfl

/-- Create a morphism between partial map diagrams by providing a morphism `g : U‚ÇÅ ‚ü∂ U‚ÇÇ`
  which makes the obvious triangles commute -/
def homMk {X Y : C} {f‚ÇÅ f‚ÇÇ : X ‚áÄ' Y} (g : f‚ÇÅ.obj.pt ‚ü∂ f‚ÇÇ.obj.pt)
    (hgm : g ‚â´ f‚ÇÇ.obj.fst = f‚ÇÅ.obj.fst := by cat_disch)
    (hgf : g ‚â´ f‚ÇÇ.obj.snd = f‚ÇÅ.obj.snd := by cat_disch) :
    f‚ÇÅ ‚ü∂ f‚ÇÇ := ObjectProperty.homMk
  {
    hom := g
    w j := by
      match j with
      | .mk .left => exact hgm
      | .mk .right => exact hgf}

@[simp]
lemma homMk_hom_hom {X Y : C} (f‚ÇÅ f‚ÇÇ : X ‚áÄ' Y) (g : f‚ÇÅ.obj.pt ‚ü∂ f‚ÇÇ.obj.pt)
    (hgm : g ‚â´ f‚ÇÇ.obj.fst = f‚ÇÅ.obj.fst) (hgf : g ‚â´ f‚ÇÇ.obj.snd = f‚ÇÅ.obj.snd) :
    (homMk g hgm hgf).hom.hom = g := rfl

@[simp]
lemma _root_.CategoryTheory.Limits.ConeMorphism.w_left {X Y : C} {f g : X ‚áÄ' Y} (h : f ‚ü∂ g) :
  h.hom.hom ‚â´ g.obj.fst = f.obj.fst := h.hom.w (.mk .left)

@[simp]
lemma _root_.CategoryTheory.Limits.ConeMorphism.w_right {X Y : C} {f g : X ‚áÄ' Y} (h : f ‚ü∂ g) :
  h.hom.hom ‚â´ g.obj.snd = f.obj.snd := h.hom.w (.mk .right)

/-- The category is thin, so all maps are equal. -/
@[ext]
lemma hom_ext {X Y : C} {f g : X ‚áÄ' Y} (h‚ÇÅ h‚ÇÇ : f ‚ü∂ g) :
    h‚ÇÅ = h‚ÇÇ := by
  apply Subsingleton.elim

@[simp]
lemma eqToHom_hom {X Y : C} {f g : X ‚áÄ' Y} (h : f = g) :
    (eqToHom h).hom = eqToHom ((congr(($h).obj))) := by
  cases h; dsimp [(ùüô ¬∑)]

@[simp]
lemma homMk_eta {X Y : C} {f g : X ‚áÄ' Y} (h : f ‚ü∂ g) :
    homMk (h.hom.hom) (h.hom.w (.mk .left)) (h.hom.w (.mk .right)) = h := by
  ext

-- not too sure about the use of this
@[simp]
lemma homMk_id {X Y U‚ÇÅ : C} {m‚ÇÅ : U‚ÇÅ ‚ü∂ X} [Mono m‚ÇÅ] {f‚ÇÅ : U‚ÇÅ ‚ü∂ Y} :
  homMk (ùüô U‚ÇÅ) = ùüô (mk m‚ÇÅ f‚ÇÅ) := rfl

@[simp]
lemma homMk_id' {X Y : C} (f : X ‚áÄ' Y) :
  homMk (ùüô f.obj.pt) = ùüô f := rfl


@[reassoc (attr := simp)]
lemma homMk_comp {X Y : C} {f‚ÇÅ f‚ÇÇ f‚ÇÉ : X ‚áÄ' Y} (g‚ÇÅ : f‚ÇÅ.obj.pt ‚ü∂ f‚ÇÇ.obj.pt)
    (hgm‚ÇÅ : g‚ÇÅ ‚â´ f‚ÇÇ.obj.fst = f‚ÇÅ.obj.fst) (hgf‚ÇÅ : g‚ÇÅ ‚â´ f‚ÇÇ.obj.snd = f‚ÇÅ.obj.snd)
    (g‚ÇÇ : f‚ÇÇ.obj.pt ‚ü∂ f‚ÇÉ.obj.pt) (hgm‚ÇÇ : g‚ÇÇ ‚â´ f‚ÇÉ.obj.fst = f‚ÇÇ.obj.fst)
    (hgf‚ÇÇ : g‚ÇÇ ‚â´ f‚ÇÉ.obj.snd = f‚ÇÇ.obj.snd) :
    homMk g‚ÇÅ hgm‚ÇÅ hgf‚ÇÅ ‚â´ homMk g‚ÇÇ hgm‚ÇÇ hgf‚ÇÇ = homMk (g‚ÇÅ ‚â´ g‚ÇÇ) := rfl

/-- The functor from the category of partial map diagrams to the category of subobject diagrams -/
def overMono {X Y : C} : X ‚áÄ' Y ‚•§ MonoOver X where
  obj f := ‚ü®(Over.mk f.obj.fst),f.property‚ü©
  map g :=
  ObjectProperty.homMk <| Over.homMk (g.hom.hom) (g.hom.w (.mk .left))

/-- The functor from the category of partial map diagrams from `X` to `Y` to the
  over-category `C/Y`. -/
def over {X Y : C} : X ‚áÄ' Y ‚•§ Over Y where
  obj f := Over.mk f.obj.snd
  map g := Over.homMk (g.hom.hom)

variable [HasPullbacks C]

/-- Composition of partial map diagrams -/
noncomputable def comp {X Y Z : C} (f : X ‚áÄ' Y) (g : Y ‚áÄ' Z) : X ‚áÄ' Z :=
  PrePartialMap.mk (pullback.fst f.obj.snd g.obj.fst ‚â´ f.obj.fst) (pullback.snd _ _ ‚â´ g.obj.snd)

noncomputable def mkCompMkIso {X Y Z : C} {U‚ÇÅ : C} (m‚ÇÅ : U‚ÇÅ ‚ü∂ X) [Mono m‚ÇÅ] (f‚ÇÅ : U‚ÇÅ ‚ü∂ Y)
    {U‚ÇÇ : C} (m‚ÇÇ : U‚ÇÇ ‚ü∂ Y) [Mono m‚ÇÇ] (f‚ÇÇ : U‚ÇÇ ‚ü∂ Z) {U‚ÇÉ : C} {m‚ÇÉ : U‚ÇÉ ‚ü∂ U‚ÇÅ} {f‚ÇÉ : U‚ÇÉ ‚ü∂ U‚ÇÇ}
    (h : IsPullback m‚ÇÉ f‚ÇÉ f‚ÇÅ m‚ÇÇ) :
    letI : Mono m‚ÇÉ := h.mono_fst
    comp (mk m‚ÇÅ f‚ÇÅ) (mk m‚ÇÇ f‚ÇÇ) ‚âÖ mk (m‚ÇÉ ‚â´ m‚ÇÅ) (f‚ÇÉ ‚â´ f‚ÇÇ) where
  hom := homMk (h.isoPullback.inv)
  inv := homMk (h.isoPullback.hom) (by simp [comp]) (by simp [comp])
  hom_inv_id := by ext
  inv_hom_id := by ext

noncomputable def mkOfHomCompIso {X Y Z : C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) :
    mkOfHom (f ‚â´ g) ‚âÖ comp (mkOfHom f) (mkOfHom g) :=
  eqToIso (by simpa using refl (mkOfHom (f ‚â´ g))) ‚â™‚â´
    (mkCompMkIso (ùüô X) f (ùüô Y) g (IsPullback.id_horiz f)).symm

noncomputable def mkOfMonoCompIso {X Y Z : C} (m‚ÇÅ : Y ‚ü∂ X) [Mono m‚ÇÅ] (m‚ÇÇ : Z ‚ü∂ Y) [Mono m‚ÇÇ] :
    mkOfMono (m‚ÇÇ ‚â´ m‚ÇÅ) ‚âÖ comp (mkOfMono m‚ÇÅ) (mkOfMono m‚ÇÇ) :=
  eqToIso (by simpa using refl (mkOfMono (m‚ÇÇ ‚â´ m‚ÇÅ))) ‚â™‚â´
    (mkCompMkIso m‚ÇÅ (ùüô Y) m‚ÇÇ (ùüô Z) (IsPullback.id_vert m‚ÇÇ)).symm

noncomputable def mkOfMonoCompMkOfHomIso {X Y U : C} (m : U ‚ü∂ X) [Mono m] (f : U ‚ü∂ Y) :
    comp (mkOfMono m) (mkOfHom f) ‚âÖ mk m f :=
  mkCompMkIso m (ùüô U) (ùüô U) f (IsPullback.id_vert (ùüô U)) ‚â™‚â´
    eqToIso (by simp)

/-- The associator iso in the bicategory of partial map diagrams -/
noncomputable def associator {W X Y Z : C} (f‚ÇÅ : W ‚áÄ' X) (f‚ÇÇ : X ‚áÄ' Y) (f‚ÇÉ : Y ‚áÄ' Z) :
    comp (comp f‚ÇÅ f‚ÇÇ) f‚ÇÉ ‚âÖ comp f‚ÇÅ (comp f‚ÇÇ f‚ÇÉ) where
  hom := homMk
    (pullback.lift
      (pullback.fst _ _ ‚â´ pullback.fst _ _)
      (pullback.map _ _ _ _ (pullback.snd _ _) (ùüô f‚ÇÉ.obj.pt) (ùüô Y) (by simp [comp]) (by simp))
      (by simp [comp, pullback.condition]))
    (by simp [comp])
    (by simp [comp])
  inv := homMk
    (pullback.lift
      (pullback.map _ _ _ _ (ùüô f‚ÇÅ.obj.pt) (pullback.fst _ _) (ùüô X) (by simp) (by simp [comp]))
      (pullback.snd _ _ ‚â´ pullback.snd _ _)
      (by simp [comp,pullback.condition]))
    (by simp [comp])
    (by simp [comp])
  hom_inv_id := by ext
  inv_hom_id := by ext

/-- left whiskering in the bicategory of partial map diagrams -/
noncomputable def whiskerLeft {X Y Z : C} (f : X ‚áÄ' Y) {g‚ÇÅ g‚ÇÇ : Y ‚áÄ' Z} (h : g‚ÇÅ ‚ü∂ g‚ÇÇ) :
    comp f g‚ÇÅ ‚ü∂ comp f g‚ÇÇ :=
  homMk (pullback.map (f.obj.snd) g‚ÇÅ.obj.fst f.obj.snd g‚ÇÇ.obj.fst (ùüô f.obj.pt) h.hom.hom (ùüô Y)
    (by simp) (by simp)) (by simp [comp]) (by simp [comp])

/-- right whiskering in the bicategory of partial map diagrams -/
noncomputable def whiskerRight {X Y Z : C} {f‚ÇÅ f‚ÇÇ : X ‚áÄ' Y} (h : f‚ÇÅ ‚ü∂ f‚ÇÇ) (g : Y ‚áÄ' Z) :
    comp f‚ÇÅ g ‚ü∂ comp f‚ÇÇ g :=
  homMk (pullback.map f‚ÇÅ.obj.snd g.obj.fst f‚ÇÇ.obj.snd g.obj.fst h.hom.hom (ùüô g.obj.pt) (ùüô Y)
    (by simp) (by simp)) (by simp [comp]) (by simp [comp])

/-- the left unitor in the bicategory of partial map diagrams. -/
noncomputable def leftUnitor {X Y : C} (f : X ‚áÄ' Y) : comp (mkOfHom (ùüô X)) f ‚âÖ f where
  hom := homMk (pullback.snd _ _) (pullback.condition.symm) rfl
  inv := homMk (pullback.lift f.obj.fst (ùüô f.obj.pt) (by simp [mkOfHom])) (by simp [mkOfHom, comp])
    (by simp [comp])
  hom_inv_id := by ext
  inv_hom_id := by ext

/-- the right unitor in the bicategory of partial map diagrams. -/
noncomputable def rightUnitor {X Y : C} (f : X ‚áÄ' Y) : comp f (mkOfHom (ùüô Y)) ‚âÖ f where
  hom := homMk (pullback.fst _ _) (rfl) (pullback.condition)
  inv := homMk (pullback.lift (ùüô f.obj.pt) f.obj.snd) (by simp [comp]) (by simp [comp])
  hom_inv_id := by ext
  inv_hom_id := by ext

@[simp]
lemma id_whiskerLeft {X Y : C} {f‚ÇÅ f‚ÇÇ : X ‚áÄ' Y} (h : f‚ÇÅ ‚ü∂ f‚ÇÇ) :
    whiskerLeft (mkOfHom (ùüô X)) h = (leftUnitor f‚ÇÅ).hom ‚â´ h ‚â´ (leftUnitor f‚ÇÇ).inv := by
  ext

@[simp]
lemma whiskerLeft_id {X Y Z : C} (f : X ‚áÄ' Y) (g : Y ‚áÄ' Z) :
    whiskerLeft f (ùüô g) = ùüô (comp f g) := by
  simpa [whiskerLeft] using hom_ext _ _
  -- ext

@[simp]
lemma whiskerLeft_comp {X Y Z : C} (f : X ‚áÄ' Y) {g‚ÇÅ g‚ÇÇ g‚ÇÉ : Y ‚áÄ' Z}
    (h‚ÇÅ : g‚ÇÅ ‚ü∂ g‚ÇÇ) (h‚ÇÇ : g‚ÇÇ ‚ü∂ g‚ÇÉ) :
    whiskerLeft f (h‚ÇÅ ‚â´ h‚ÇÇ) = whiskerLeft f h‚ÇÅ ‚â´ whiskerLeft f h‚ÇÇ := by
  simpa [whiskerLeft] using hom_ext _ _

@[simp]
lemma comp_whiskerLeft {W X Y Z : C} (f‚ÇÅ : W ‚áÄ' X) (f‚ÇÇ : X ‚áÄ' Y) {g‚ÇÅ g‚ÇÇ : Y ‚áÄ' Z}
    (h : g‚ÇÅ ‚ü∂ g‚ÇÇ) : whiskerLeft (comp f‚ÇÅ f‚ÇÇ) h = (associator f‚ÇÅ f‚ÇÇ g‚ÇÅ).hom ‚â´
      whiskerLeft f‚ÇÅ (whiskerLeft f‚ÇÇ h) ‚â´ (associator f‚ÇÅ f‚ÇÇ g‚ÇÇ).inv := by
  ext

lemma whiskerRight_id {X Y : C} {f‚ÇÅ f‚ÇÇ : X ‚áÄ' Y} (h : f‚ÇÅ ‚ü∂ f‚ÇÇ) :
    whiskerRight h (mkOfHom (ùüô Y)) = (rightUnitor f‚ÇÅ).hom ‚â´ h ‚â´ (rightUnitor f‚ÇÇ).inv := by
  ext

@[simp]
lemma id_whiskerRight {X Y Z : C} (f : X ‚áÄ' Y) (g : Y ‚áÄ' Z) :
    whiskerRight (ùüô f) g = ùüô (comp f g) := by
  ext

@[simp]
lemma comp_whiskerRight {X Y Z : C} {f‚ÇÅ f‚ÇÇ f‚ÇÉ : X ‚áÄ' Y} (h‚ÇÅ : f‚ÇÅ ‚ü∂ f‚ÇÇ) (h‚ÇÇ : f‚ÇÇ ‚ü∂ f‚ÇÉ)
    (g : Y ‚áÄ' Z) : whiskerRight (h‚ÇÅ ‚â´ h‚ÇÇ) g = whiskerRight h‚ÇÅ g ‚â´ whiskerRight h‚ÇÇ g := by
  ext

@[simp]
lemma whiskerRight_comp {W X Y Z : C} {f‚ÇÅ f‚ÇÇ : W ‚áÄ' X} (h : f‚ÇÅ ‚ü∂ f‚ÇÇ) (g‚ÇÅ : X ‚áÄ' Y)
    (g‚ÇÇ : Y ‚áÄ' Z) : whiskerRight h (comp g‚ÇÅ g‚ÇÇ) = (associator f‚ÇÅ g‚ÇÅ g‚ÇÇ).inv ‚â´
      whiskerRight (whiskerRight h g‚ÇÅ) g‚ÇÇ ‚â´ (associator f‚ÇÇ g‚ÇÅ g‚ÇÇ).hom := by
  ext

lemma whisker_assoc {W X Y Z : C} (f‚ÇÅ : W ‚áÄ' X) {g‚ÇÅ g‚ÇÇ : X ‚áÄ' Y} (h : g‚ÇÅ ‚ü∂ g‚ÇÇ)
    (f‚ÇÇ : Y ‚áÄ' Z) : whiskerRight (whiskerLeft f‚ÇÅ h) f‚ÇÇ = (associator f‚ÇÅ g‚ÇÅ f‚ÇÇ).hom ‚â´
    whiskerLeft f‚ÇÅ (whiskerRight h f‚ÇÇ) ‚â´ (associator f‚ÇÅ g‚ÇÇ f‚ÇÇ).inv := by
  ext

lemma whisker_exchange {X Y Z : C} {f‚ÇÅ f‚ÇÇ : X ‚áÄ' Y} (f : f‚ÇÅ ‚ü∂ f‚ÇÇ)
    {g‚ÇÅ g‚ÇÇ : Y ‚áÄ' Z} (g : g‚ÇÅ ‚ü∂ g‚ÇÇ) : whiskerLeft f‚ÇÅ g ‚â´ (whiskerRight f g‚ÇÇ) =
    whiskerRight f g‚ÇÅ ‚â´ whiskerLeft f‚ÇÇ g := by
  ext

lemma pentagon {A B D E F : C} (f : A ‚áÄ' B) (g : B ‚áÄ' D) (h : D ‚áÄ' E) (i : E ‚áÄ' F) :
    whiskerRight (associator f g h).hom i ‚â´ (associator f (comp g h) i).hom ‚â´
      whiskerLeft f (associator g h i).hom =
      (associator (comp f g) h i).hom ‚â´ (associator f g (comp h i)).hom := by
  ext

lemma triangle {X Y Z : C} (f : X ‚áÄ' Y) (g : Y ‚áÄ' Z) :
    (associator f (mkOfHom (ùüô Y)) g).hom ‚â´ whiskerLeft f (leftUnitor g).hom =
      whiskerRight (rightUnitor f).hom g := by
  ext

noncomputable instance : Bicategory (WithPrePartialMaps C) where
  id X := mkOfHom (ùüô X.out)
  comp {X Y Z} f g := comp f g
  whiskerLeft {X Y Z} f g‚ÇÅ g‚ÇÇ h := whiskerLeft f h
  whiskerRight {X Y Z} f‚ÇÅ f‚ÇÇ h g := whiskerRight h g
  associator {W X Y Z} f g h := associator f g h
  leftUnitor {X Y} f := leftUnitor f
  rightUnitor {X Y} f := rightUnitor f
  whiskerLeft_id := whiskerLeft_id
  whiskerLeft_comp := whiskerLeft_comp
  id_whiskerLeft := id_whiskerLeft
  comp_whiskerLeft := comp_whiskerLeft
  id_whiskerRight := id_whiskerRight
  comp_whiskerRight := comp_whiskerRight
  whiskerRight_id := whiskerRight_id
  whiskerRight_comp := whiskerRight_comp
  whisker_assoc := whisker_assoc
  whisker_exchange {X Y Z} f‚ÇÅ f‚ÇÇ g‚ÇÅ g‚ÇÇ f g := whisker_exchange f g
  pentagon := pentagon
  triangle := triangle

end PrePartialMap

/-- The skeleton category of partially defined maps, where given `f g : X ‚áÄ Y`,
  the map `f ‚ü∂ g` exists iff the support of `g` contains the support of `f` and
  the maps agree on the support of `f` -/
def PartialMap (X Y : C) := ThinSkeleton (X ‚áÄ' Y)
namespace PartialMap

variable (C) in
/-- The 2-category `C` with partial maps in `C` as morphisms. -/
structure _root_.CategoryTheory.WithPartialMaps : Type u where
  mk :: (out : C)

attribute [pp_nodot] WithPartialMaps.mk

-- TODO : Modulize, make meta
@[app_unexpander WithPartialMaps.mk]
protected def WithPartialMaps.unexpander_mk : Lean.PrettyPrinter.Unexpander
  | s => pure s

instance : Quiver (WithPartialMaps C) where
  Hom X Y := PartialMap X.out Y.out

-- instance (X Y : WithPartialMaps C) : Category (X ‚ü∂ Y) := inferInstanceAs
--   (Category (ThinSkeleton _))

-- not sure if this is the right precedence yet. it should be more than 40, in order to parse
-- correctly w/r/t "=". see also the notation "‚áÄ'"
notation:40 X:41 " ‚áÄ " Y:41 => WithPartialMaps.mk X ‚ü∂ WithPartialMaps.mk Y

instance {X Y : WithPartialMaps C} : PartialOrder (X ‚ü∂ Y) :=
  inferInstanceAs (PartialOrder (ThinSkeleton (X.out ‚áÄ' Y.out)))

def mk {U X Y : C} (m : U ‚ü∂ X) [Mono m] (f : U ‚ü∂ Y) : X ‚áÄ Y :=
  ThinSkeleton.mk (.mk m f)

lemma le_iff {X Y U‚ÇÅ : C} {m‚ÇÅ : U‚ÇÅ ‚ü∂ X} [Mono m‚ÇÅ] {f‚ÇÅ : U‚ÇÅ ‚ü∂ Y}
    {U‚ÇÇ : C} {m‚ÇÇ : U‚ÇÇ ‚ü∂ X} [Mono m‚ÇÇ] {f‚ÇÇ : U‚ÇÇ ‚ü∂ Y} : mk m‚ÇÅ f‚ÇÅ ‚â§ mk m‚ÇÇ f‚ÇÇ ‚Üî
    ‚àÉ f : U‚ÇÅ ‚ü∂ U‚ÇÇ, f ‚â´ m‚ÇÇ = m‚ÇÅ ‚àß f ‚â´ f‚ÇÇ = f‚ÇÅ := by
  dsimp [(¬∑ ‚â§ ¬∑)]
  dsimp [mk, ThinSkeleton.mk, Quotient.mk']
  constructor
  ¬∑ rintro ‚ü®z‚ü©
    use z.hom.hom, z.hom.w (.mk .left)
    exact z.hom.w (.mk .right)
  ¬∑ rintro ‚ü®f,hf‚ÇÅ,hf‚ÇÇ‚ü©
    exact ‚ü®PrePartialMap.homMk f hf‚ÇÅ hf‚ÇÇ‚ü©

@[simp]
lemma mk_obj {X Y : C} (x : PrePartialMap X Y) :
  PartialMap.mk x.obj.fst x.obj.snd = ‚ü¶x‚üß := by
  dsimp [mk]
  congr
  exact PrePartialMap.mk_obj x

lemma mk_eq {U‚ÇÅ U‚ÇÇ X Y : C} (m‚ÇÅ : U‚ÇÅ ‚ü∂ X) [Mono m‚ÇÅ] (f‚ÇÅ : U‚ÇÅ ‚ü∂ Y)
    (m‚ÇÇ : U‚ÇÇ ‚ü∂ X) [Mono m‚ÇÇ] (f‚ÇÇ : U‚ÇÇ ‚ü∂ Y) : mk m‚ÇÅ f‚ÇÅ = mk m‚ÇÇ f‚ÇÇ ‚Üî
    ‚àÉ e : U‚ÇÅ ‚âÖ U‚ÇÇ, e.hom ‚â´ m‚ÇÇ = m‚ÇÅ ‚àß e.hom ‚â´ f‚ÇÇ = f‚ÇÅ := by
  constructor
  ¬∑ intro h
    have := Quotient.eq.mp h
    simp only [isIsomorphicSetoid, IsIsomorphic] at this
    obtain ‚ü®e'‚ü© := this
    use ‚ü®e'.hom.hom.hom,e'.inv.hom.hom,congr($(e'.hom_inv_id).hom.hom),
      congr($(e'.inv_hom_id).hom.hom)‚ü©
    simp only
    constructor
    ¬∑ simpa using e'.hom.hom.w (.mk .left)
    ¬∑ simpa using e'.hom.hom.w (.mk .right)
  ¬∑ rintro ‚ü®e,he‚ÇÅ, he‚ÇÇ‚ü©
    apply Quotient.sound
    constructor
    refine (ObjectProperty.IsPartialMap X Y).isoMk ?_
    apply BinaryFan.ext e <;> simp [PrePartialMap.mk, he‚ÇÅ, he‚ÇÇ]

def rec {X Y : C} {motive : X ‚áÄ Y ‚Üí Sort*}
    (ofMk : ‚àÄ {U : C} (m : U ‚ü∂ X) [Mono m] (f : U ‚ü∂ Y), motive (.mk m f))
    (ofMk_sound : ‚àÄ {U‚ÇÅ : C} (m‚ÇÅ : U‚ÇÅ ‚ü∂ X) [Mono m‚ÇÅ] (f‚ÇÅ : U‚ÇÅ ‚ü∂ Y),
      ‚àÄ {U‚ÇÇ : C} (m‚ÇÇ : U‚ÇÇ ‚ü∂ X) [Mono m‚ÇÇ] (f‚ÇÇ : U‚ÇÇ ‚ü∂ Y),
      (h : mk m‚ÇÅ f‚ÇÅ = mk m‚ÇÇ f‚ÇÇ) ‚Üí ofMk m‚ÇÅ f‚ÇÅ = h ‚ñ∏ ofMk m‚ÇÇ f‚ÇÇ) :
    ‚àÄ (f : X ‚áÄ Y), motive f :=
  Quotient.rec (fun a => ((PartialMap.mk_obj (C := C) a).symm) ‚ñ∏ ofMk (a.obj.fst) (a.obj.snd))
    (by
    intro a b _
    generalize_proofs _ _ h‚ÇÅ hab _ _ h‚ÇÇ
    rw [ofMk_sound a.obj.fst a.obj.snd b.obj.fst b.obj.snd ((h‚ÇÅ.trans hab).trans (h‚ÇÇ.symm))]
    convert rfl
    ¬∑ rw [h‚ÇÇ,hab]
    ¬∑ simp
    )

def rec' {X Y : C} {motive : X ‚áÄ Y ‚Üí Sort*}
    (ofMk : ‚àÄ {U : C} (m : U ‚ü∂ X) [Mono m] (f : U ‚ü∂ Y), motive (.mk m f))
    [‚àÄ (f : X ‚áÄ Y), Subsingleton (motive f)] :
    ‚àÄ (f : X ‚áÄ Y), motive f := PartialMap.rec (ofMk) (by intros; apply Subsingleton.elim)

lemma rec'_mk {X Y : C} {motive : X ‚áÄ Y ‚Üí Sort*}
    (ofMk : ‚àÄ {U : C} (m : U ‚ü∂ X) [Mono m] (f : U ‚ü∂ Y), motive (.mk m f))
    [‚àÄ (f : X ‚áÄ Y), Subsingleton (motive f)]
    {U : C} (m : U ‚ü∂ X) [Mono m] (f : U ‚ü∂ Y) :
  PartialMap.rec' ofMk (mk m f) = ofMk m f := rfl

def lift {X Y : C} {motive : Sort*}
    (ofMk : ‚àÄ {U : C} (m : U ‚ü∂ X) [Mono m] (_f : U ‚ü∂ Y), motive)
    (ofMk_sound : ‚àÄ {U‚ÇÅ : C} (m‚ÇÅ : U‚ÇÅ ‚ü∂ X) [Mono m‚ÇÅ] (f‚ÇÅ : U‚ÇÅ ‚ü∂ Y),
      ‚àÄ {U‚ÇÇ : C} (m‚ÇÇ : U‚ÇÇ ‚ü∂ X) [Mono m‚ÇÇ] (f‚ÇÇ : U‚ÇÇ ‚ü∂ Y),
      (h : mk m‚ÇÅ f‚ÇÅ = mk m‚ÇÇ f‚ÇÇ) ‚Üí ofMk m‚ÇÅ f‚ÇÅ = ofMk m‚ÇÇ f‚ÇÇ) :
    X ‚áÄ Y ‚Üí motive :=
  PartialMap.rec (ofMk) (fun {U‚ÇÅ} m‚ÇÅ _ f‚ÇÅ {U‚ÇÇ} m‚ÇÇ _ f‚ÇÇ h =>
    (by simp [ofMk_sound m‚ÇÅ f‚ÇÅ m‚ÇÇ f‚ÇÇ h]))

@[simp]
lemma rec_mk {X Y : C} {motive : X ‚áÄ Y ‚Üí Sort*}
    (ofMk : ‚àÄ {U : C} (m : U ‚ü∂ X) [Mono m] (f : U ‚ü∂ Y), motive (.mk m f))
    (ofMk_sound : ‚àÄ {U‚ÇÅ : C} (m‚ÇÅ : U‚ÇÅ ‚ü∂ X) [Mono m‚ÇÅ] (f‚ÇÅ : U‚ÇÅ ‚ü∂ Y),
      ‚àÄ {U‚ÇÇ : C} (m‚ÇÇ : U‚ÇÇ ‚ü∂ X) [Mono m‚ÇÇ] (f‚ÇÇ : U‚ÇÇ ‚ü∂ Y),
      (h : mk m‚ÇÅ f‚ÇÅ = mk m‚ÇÇ f‚ÇÇ) ‚Üí ofMk m‚ÇÅ f‚ÇÅ = h ‚ñ∏ ofMk m‚ÇÇ f‚ÇÇ)
    {U : C} (m : U ‚ü∂ X) [Mono m] (f : U ‚ü∂ Y) :
    PartialMap.rec ofMk ofMk_sound (mk m f) = ofMk m f := rfl

@[simp]
lemma lift_mk {X Y : C} {motive : Sort*}
    (ofMk : ‚àÄ {U : C} (m : U ‚ü∂ X) [Mono m] (_f : U ‚ü∂ Y), motive)
    (ofMk_sound : ‚àÄ {U‚ÇÅ : C} (m‚ÇÅ : U‚ÇÅ ‚ü∂ X) [Mono m‚ÇÅ] (f‚ÇÅ : U‚ÇÅ ‚ü∂ Y),
      ‚àÄ {U‚ÇÇ : C} (m‚ÇÇ : U‚ÇÇ ‚ü∂ X) [Mono m‚ÇÇ] (f‚ÇÇ : U‚ÇÇ ‚ü∂ Y),
      (h : mk m‚ÇÅ f‚ÇÅ = mk m‚ÇÇ f‚ÇÇ) ‚Üí ofMk m‚ÇÅ f‚ÇÅ = ofMk m‚ÇÇ f‚ÇÇ)
    {U : C} (m : U ‚ü∂ X) [Mono m] (f : U ‚ü∂ Y) :
    PartialMap.lift ofMk ofMk_sound (mk m f) = ofMk m f :=
  rfl

@[cases_eliminator, induction_eliminator]
lemma induction {X Y : C} {motive : (X ‚áÄ Y) ‚Üí Prop}
    (h_mk : ‚àÄ {U : C} (m : U ‚ü∂ X) [Mono m] (f : U ‚ü∂ Y), motive (.mk m f)) :
    ‚àÄ f, motive f :=
  PartialMap.rec (h_mk) (by simp)

lemma induction‚ÇÇ {X Y : C} {motive : (X ‚áÄ Y) ‚Üí (X ‚áÄ Y) ‚Üí Prop}
    (h_mk : ‚àÄ {U‚ÇÅ : C} (m‚ÇÅ : U‚ÇÅ ‚ü∂ X) [Mono m‚ÇÅ] (f‚ÇÅ : U‚ÇÅ ‚ü∂ Y),
      ‚àÄ {U‚ÇÇ : C} (m‚ÇÇ : U‚ÇÇ ‚ü∂ X) [Mono m‚ÇÇ] (f‚ÇÇ : U‚ÇÇ ‚ü∂ Y),
      motive (.mk m‚ÇÅ f‚ÇÅ) (.mk m‚ÇÇ f‚ÇÇ)) :
    ‚àÄ f‚ÇÅ f‚ÇÇ, motive f‚ÇÅ f‚ÇÇ := by
  intro f‚ÇÅ f‚ÇÇ
  induction f‚ÇÅ with
  | h_mk m‚ÇÅ f‚ÇÅ =>
    induction f‚ÇÇ with
    | h_mk m‚ÇÇ f‚ÇÇ =>
      exact h_mk m‚ÇÅ f‚ÇÅ m‚ÇÇ f‚ÇÇ

/-- The domain of a partial map -/
protected def support {X Y : C} : X ‚áÄ Y ‚•§ Subobject X :=
  ThinSkeleton.map (PrePartialMap.overMono)

lemma support.obj_mk {X Y : C} {U : C} (m : U ‚ü∂ X) [Mono m] (f : U ‚ü∂ Y) :
  PartialMap.support.obj (mk m f) = Subobject.mk m := rfl

variable [HasPullbacks C]

noncomputable def comp {X Y Z : C} : X ‚áÄ Y ‚•§ Y ‚áÄ Z ‚•§ X ‚áÄ Z :=
  ThinSkeleton.map‚ÇÇ (Bicategory.precomposing
    (WithPrePartialMaps.mk X) (WithPrePartialMaps.mk Y) (WithPrePartialMaps.mk Z))

def ofHom {X Y : C} (f : X ‚ü∂ Y) : X ‚áÄ Y := mk (ùüô _) f

omit [HasPullbacks C] in
lemma ofHom_eq_mk {X Y : C} (f : X ‚ü∂ Y) : ofHom f = mk (ùüô _) f := rfl


def ofMono {X Y : C} (m : Y ‚ü∂ X) [Mono m] : X ‚áÄ Y := mk m (ùüô _)

omit [HasPullbacks C] in
lemma ofMono_eq_mk {X Y : C} (m : Y ‚ü∂ X) [Mono m] : ofMono m = mk m (ùüô _) := rfl


def id (X : C) : X ‚áÄ X := ofHom (ùüô X)

omit [HasPullbacks C] in
lemma id_eq (X : C) : id X = mk (ùüô X) (ùüô X) :=
  rfl

end PartialMap

namespace WithPartialMaps
/-- all this to say, `WithPartialMaps C` is a 2-category -/
noncomputable instance [HasPullbacks C] : Bicategory (WithPartialMaps C) where
  id {X} := PartialMap.id X.out
  comp {X Y Z} f g := (PartialMap.comp.obj f).obj g
  whiskerLeft {X Y Z} f {g‚ÇÅ g‚ÇÇ} h := (PartialMap.comp.obj f).map h
  whiskerRight {X Y Z} {f‚ÇÅ f‚ÇÇ} h g := (PartialMap.comp.map h).app g
  associator {W X Y Z} := PartialMap.rec'
    (fun m‚ÇÅ _ f‚ÇÅ => PartialMap.rec'
      (fun m‚ÇÇ _ f‚ÇÇ => PartialMap.rec'
        (fun m‚ÇÉ _ f‚ÇÉ =>
          eqToIso (Quotient.sound ‚ü®
            (PrePartialMap.associator
            (PrePartialMap.mk m‚ÇÅ f‚ÇÅ)
            (PrePartialMap.mk m‚ÇÇ f‚ÇÇ)
            (PrePartialMap.mk m‚ÇÉ f‚ÇÉ))‚ü©))))
  leftUnitor {X Y} := PartialMap.rec'
    (fun m‚ÇÅ _ f‚ÇÅ => eqToIso (Quotient.sound ‚ü®PrePartialMap.leftUnitor (PrePartialMap.mk m‚ÇÅ f‚ÇÅ)‚ü©))
  rightUnitor {X Y} := PartialMap.rec'
    (fun m‚ÇÅ _ f‚ÇÅ => eqToIso (Quotient.sound ‚ü®PrePartialMap.rightUnitor (PrePartialMap.mk m‚ÇÅ f‚ÇÅ)‚ü©))

instance [HasPullbacks C] : Bicategory.Strict (WithPartialMaps C) where
  id_comp := PartialMap.induction (fun m _ f =>
      Quotient.sound ‚ü®PrePartialMap.leftUnitor (.mk m f)‚ü©)
  comp_id := PartialMap.induction (fun m _ f =>
    Quotient.sound ‚ü®PrePartialMap.rightUnitor (.mk m f)‚ü©)
  assoc := PartialMap.induction (fun m‚ÇÅ _ f‚ÇÅ =>
    PartialMap.induction (fun m‚ÇÇ _ f‚ÇÇ =>
      PartialMap.induction (fun m‚ÇÉ _ f‚ÇÉ =>
        Quotient.sound ‚ü®PrePartialMap.associator (.mk m‚ÇÅ f‚ÇÅ) (.mk m‚ÇÇ f‚ÇÇ) (.mk m‚ÇÉ f‚ÇÉ)‚ü©)))
  leftUnitor_eqToIso := PartialMap.induction (fun _ _ _ => rfl)
  rightUnitor_eqToIso := PartialMap.induction (fun _ _ _ => rfl)
  associator_eqToIso := PartialMap.induction (fun _ _ _ => PartialMap.induction
    (fun _ _ _ => PartialMap.induction (fun _ _ _ => rfl)))

end WithPartialMaps
namespace PartialMap
variable [HasPullbacks C]

lemma mk_comp_mk_of_isPullback {X Y Z U‚ÇÅ : C} (m‚ÇÅ : U‚ÇÅ ‚ü∂ X) [Mono m‚ÇÅ] (f‚ÇÅ : U‚ÇÅ ‚ü∂ Y)
    {U‚ÇÇ : C} (m‚ÇÇ : U‚ÇÇ ‚ü∂ Y) [Mono m‚ÇÇ] (f‚ÇÇ : U‚ÇÇ ‚ü∂ Z) {U‚ÇÉ : C} {m‚ÇÉ : U‚ÇÉ ‚ü∂ U‚ÇÅ} {f‚ÇÉ : U‚ÇÉ ‚ü∂ U‚ÇÇ}
    (h : IsPullback m‚ÇÉ f‚ÇÉ f‚ÇÅ m‚ÇÇ) :
    letI : Mono m‚ÇÉ := h.mono_fst
    mk m‚ÇÅ f‚ÇÅ ‚â´ mk m‚ÇÇ f‚ÇÇ = mk (m‚ÇÉ ‚â´ m‚ÇÅ) (f‚ÇÉ ‚â´ f‚ÇÇ) :=
  Quotient.sound ‚ü®PrePartialMap.mkCompMkIso m‚ÇÅ f‚ÇÅ m‚ÇÇ f‚ÇÇ h‚ü©

@[reassoc (attr := simp)]
lemma ofHom_comp_ofHom {X Y Z : C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) :
  (ofHom f) ‚â´ (ofHom g) = ofHom (f ‚â´ g) :=
  Quotient.sound ‚ü®(PrePartialMap.mkOfHomCompIso f g).symm‚ü©

@[reassoc (attr := simp)]
lemma ofMono_comp_ofMono {X Y Z : C} (m‚ÇÅ : Y ‚ü∂ X) [Mono m‚ÇÅ] (m‚ÇÇ : Z ‚ü∂ Y) [Mono m‚ÇÇ] :
    (ofMono m‚ÇÅ) ‚â´ (ofMono m‚ÇÇ) = ofMono (m‚ÇÇ ‚â´ m‚ÇÅ) :=
  Quotient.sound ‚ü®(PrePartialMap.mkOfMonoCompIso m‚ÇÅ m‚ÇÇ).symm‚ü©

@[reassoc (attr := simp)]
lemma ofMono_comp_ofHom {X Y U : C} (m : U ‚ü∂ X) [Mono m] (f : U ‚ü∂ Y) :
    (ofMono m) ‚â´ (ofHom f) = mk m f :=
  Quotient.sound ‚ü®PrePartialMap.mkOfMonoCompMkOfHomIso m f‚ü©

@[reassoc (attr := simp)]
lemma mk_comp_ofHom {X Y Z : C} {U : C} (m : U ‚ü∂ X) [Mono m] (f : U ‚ü∂ Y) (g : Y ‚ü∂ Z) :
    (mk m f) ‚â´ (ofHom g) = (mk m (f ‚â´ g)) := by
  rw [‚Üê ofMono_comp_ofHom,Category.assoc,ofHom_comp_ofHom, ofMono_comp_ofHom]

@[reassoc (attr := simp)]
lemma ofMono_comp_mk {X Y Z U : C} (m : U ‚ü∂ X) [Mono m] (f : U ‚ü∂ Y) (m' : X ‚ü∂ Z) [Mono m'] :
    (ofMono m') ‚â´ (mk m f) = mk (m ‚â´ m') f := by
  rw [‚Üê ofMono_comp_ofHom,‚Üê Category.assoc, ofMono_comp_ofMono, ofMono_comp_ofHom]

end PartialMap
namespace WithPartialMaps

variable (C) in
noncomputable abbrev withPartialMapsPreCore [HasPullbacks C] : StrictPseudofunctorPreCore
    (LocallyDiscrete C) (WithPartialMaps C) where
  obj X := .mk X.as
  map f := PartialMap.ofHom f.as
  map‚ÇÇ g := eqToHom (by rw [LocallyDiscrete.eq_of_hom g])
  map_comp {X Y Z} f‚ÇÅ f‚ÇÇ := Quotient.sound ‚ü®PrePartialMap.mkOfHomCompIso f‚ÇÅ.as f‚ÇÇ.as‚ü©


variable (C) in
-- @[simp]
noncomputable def _root_.CategoryTheory.withPartialMaps [HasPullbacks C] :
    StrictPseudofunctor (LocallyDiscrete C) (WithPartialMaps C) :=
  .mk'' (withPartialMapsPreCore C)

variable (C) in
@[simps]
def toLocallyDiscrete : C ‚•§ LocallyDiscrete C where
  obj X := .mk X
  map f := f.toLoc

variable (C) in
@[simps]
def ofLocallyDiscrete : LocallyDiscrete C ‚•§ C where
  obj X := X.as
  map f := f.as

@[simps]
def locallyDiscreteEquivalence : LocallyDiscrete C ‚âå C where
  functor := ofLocallyDiscrete C
  inverse := toLocallyDiscrete C
  unitIso := Iso.refl _
  counitIso := Iso.refl _

@[simp]
lemma _root_.CategoryTheory.withPartialMaps_obj [HasPullbacks C] (X : LocallyDiscrete C) :
    (withPartialMaps C).obj X = .mk X.as := rfl

@[simp]
lemma _root_.CategoryTheory.withPartialMaps_map [HasPullbacks C] {X Y : LocallyDiscrete C}
    (f : X ‚ü∂ Y) : (withPartialMaps C).map f = PartialMap.ofHom f.as := rfl

lemma ofHom_injective [HasPullbacks C] {X Y : C} :
    Function.Injective (PartialMap.ofHom (X := X) (Y := Y)) := by
  intro f g h
  simp_rw [PartialMap.ofHom_eq_mk] at h
  rw [PartialMap.mk_eq] at h
  obtain ‚ü®e,hid,hf‚ü© := h
  simp only [Category.comp_id] at hid
  rw [hid] at hf
  simpa using hf.symm

instance [HasPullbacks C] : (toLocallyDiscrete C ‚ãô (withPartialMaps C).toFunctor).Faithful where
  map_injective {_ _} := ofHom_injective

lemma mono_of_mono_ofHom [HasPullbacks C] {X Y : C} {f : X ‚ü∂ Y} :
    Mono (PartialMap.ofHom f) ‚Üí Mono f := by
  intro h
  change Mono ((toLocallyDiscrete C ‚ãô ((withPartialMaps C).toFunctor)).map f) at h
  exact Functor.ReflectsMonomorphisms.reflects _ h

lemma eq_ofHom_of_mono [HasPullbacks C] {X Y : C} (f : X ‚áÄ Y) [Mono f] :
    ‚àÉ f', f = PartialMap.ofHom f' ‚àß Mono f' := by
  have := ‚ÄπMono f‚Ä∫
  induction f with
  | h_mk m f =>
    rename_i U
    have : PartialMap.ofHom (ùüô X) ‚â´ PartialMap.mk m f = PartialMap.mk m m ‚â´ PartialMap.mk m f := by
      rw [PartialMap.ofHom_eq_mk,
        PartialMap.mk_comp_mk_of_isPullback _ _ _ _ (IsPullback.id_vert _),
        PartialMap.mk_comp_mk_of_isPullback _ _ _ _ (.of_horiz_isIso_mono (fst := ùüô _) (snd := ùüô _)
          (by simp))]
      simp
    rw [cancel_mono, PartialMap.ofHom_eq_mk, PartialMap.mk_eq] at this
    obtain ‚ü®e,he‚ÇÅ,_‚ü© := this
    use e.hom ‚â´ f
    have : PartialMap.mk m f = PartialMap.ofHom (e.hom ‚â´ f) := by
      rw [PartialMap.ofHom_eq_mk, PartialMap.mk_eq]
      use e.symm
      rw [‚Üê he‚ÇÅ]
      simp
    use this
    have : Mono (PartialMap.ofHom (e.hom ‚â´ f)) := by
      rwa [‚Üê this]
    exact mono_of_mono_ofHom this


instance mono_ofHom [HasPullbacks C] {X Y : C} (f : X ‚ü∂ Y) [Mono f] :
    Mono (C := WithPartialMaps C) (PartialMap.ofHom f) where
  right_cancellation {Z} g‚ÇÅ g‚ÇÇ h := by
    induction g‚ÇÅ, g‚ÇÇ using PartialMap.induction‚ÇÇ with
    | h_mk m‚ÇÅ f‚ÇÅ m‚ÇÇ f‚ÇÇ =>
      simp_rw [PartialMap.mk_comp_ofHom] at h
      rw [PartialMap.mk_eq] at h ‚ä¢
      apply h.imp
      intro e he
      use he.left
      rw [‚Üê Category.assoc] at he
      exact Mono.right_cancellation _ _ he.right

/-- Partial maps are monomorphisms in a category C with partial maps iff they are
  total monomorphisms -/
theorem mono_iff_exists_eq_ofHom_and_mono [HasPullbacks C] {X Y : C} (f : X ‚áÄ Y) :
  Mono f ‚Üî ‚àÉ f', (f = PartialMap.ofHom f' ‚àß Mono f') := by
  refine ‚ü®fun _ => eq_ofHom_of_mono f, by
    rintro ‚ü®f,rfl,h‚ü©
    infer_instance‚ü©
-- variable (C) in
-- @[simps]
-- def _root_.CategoryTheory.withPartialMaps [HasPullbacks C] : C ‚•§ (WithPartialMaps C) where
--   obj X := .mk X
--   map f := PartialMap.ofHom f
--   map_id _ := rfl
--   map_comp f‚ÇÅ f‚ÇÇ := Quotient.sound ‚ü®PrePartialMap.mkOfHomCompIso f‚ÇÅ f‚ÇÇ‚ü©

open Bicategory
variable (C) in
noncomputable def coyoneda [HasPullbacks C] :
    (WithPartialMaps C)·µí·µñ ‚•§ (WithPartialMaps C) ‚•§ Cat where
  obj X := {
    obj Y := Cat.of <| (X.unop) ‚ü∂ (Y)
    map {Y Z} g := (Bicategory.postcomp (X.unop) g).toCatHom
    map_id Y := by
      apply Cat.Hom.ext <| Functor.ext (by intro f; exact Category.comp_id f)
    map_comp {Y Z W} g‚ÇÅ g‚ÇÇ := by
      apply Cat.Hom.ext <| Functor.ext (by intro f; simp)
  }
  map {X‚ÇÅ X‚ÇÇ} f := {
    app Y := Functor.toCatHom {
      obj g := f.unop ‚â´ g
      map {g‚ÇÅ g‚ÇÇ} h := f.unop ‚óÅ h
      map_id g := whiskerLeft_id f.unop g
      map_comp {g‚ÇÅ g‚ÇÇ g‚ÇÇ} h‚ÇÅ h‚ÇÇ := whiskerLeft_comp f.unop h‚ÇÅ h‚ÇÇ
    }
    naturality {Y Z} g := Cat.Hom.ext <| Functor.ext (by simp)
  }
  map_id X := by
    ext Y
    exact Functor.ext (by simp)
  map_comp {X Y Z} f g := by
    ext W
    exact Functor.ext (by simp)

/-- the presheaf of partial map functors. -/
noncomputable def _root_.CategoryTheory.partialMaps
  [HasPullbacks C] : C·µí·µñ ‚•§ C ‚•§ Type _ :=
  (((Functor.whiskeringLeft‚ÇÇ (Type _)).obj (toLocallyDiscrete C ‚ãô
    (withPartialMaps C).toFunctor).op).obj
    (toLocallyDiscrete C ‚ãô (withPartialMaps C).toFunctor)).obj
    ((Functor.postcompose‚ÇÇ.obj (Cat.objects)).obj (WithPartialMaps.coyoneda C))

@[simp]
lemma _root_.CategoryTheory.partialMaps_obj_obj [HasPullbacks C] (X : C·µí·µñ) (Y : C) :
    (partialMaps.obj X).obj Y = ((WithPartialMaps.mk X.unop) ‚ü∂ (WithPartialMaps.mk (Y))) := rfl

@[simp]
lemma _root_.CategoryTheory.partialMaps_obj_map [HasPullbacks C] (X : C·µí·µñ) {Y Z : C} (g : Y ‚ü∂ Z) :
    (partialMaps.obj X).map g = (¬∑ ‚â´ (PartialMap.ofHom g)) := rfl

@[simp]
lemma _root_.CategoryTheory.partialMaps_map_app [HasPullbacks C] {X Y : C·µí·µñ} {f : Y ‚ü∂ X} (Z : C) :
    (partialMaps.map f).app Z = (PartialMap.ofHom f.unop ‚â´ ¬∑) := rfl

/-- the presheaf of partial maps into X -/
noncomputable def _root_.CategoryTheory.partialMapsTo [HasPullbacks C] (X : C) :
  C·µí·µñ ‚•§ Type _ := partialMaps.flip.obj X

@[simp]
lemma _root_.CategoryTheory.partialMapsTo_obj [HasPullbacks C] (X : C) (Y : C·µí·µñ) :
  (partialMapsTo X).obj Y = ((WithPartialMaps.mk Y.unop) ‚ü∂ (WithPartialMaps.mk X)) := rfl

@[simp]
lemma _root_.CategoryTheory.partialMapsTo_map [HasPullbacks C] (X : C) {Y Z : C·µí·µñ} (g : Y ‚ü∂ Z) :
  (partialMapsTo X).map g = (PartialMap.ofHom g.unop ‚â´ ¬∑) := rfl

noncomputable def _root_.CategoryTheory.partialMapsFrom [HasPullbacks C] (X : C) :
  C ‚•§ Type _ := partialMaps.obj (.op X)

@[simp]
lemma _root_.CategoryTheory.partialMapsFrom_obj [HasPullbacks C] (X Y : C) :
  (partialMapsFrom X).obj Y = ((WithPartialMaps.mk X) ‚ü∂ (WithPartialMaps.mk Y)) := rfl

@[simp]
lemma _root_.CategoryTheory.partialMapsFrom_map [HasPullbacks C] (X : C) {Y Z : C} (g : Y ‚ü∂ Z) :
  (partialMapsFrom X).map g = (¬∑ ‚â´ PartialMap.ofHom g) := rfl


end CategoryTheory.WithPartialMaps
